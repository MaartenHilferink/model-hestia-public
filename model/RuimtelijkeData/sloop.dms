//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) VESTA 2020 - Planbureau voor de Leefomgeving                   //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//       Deze DMS-file wordt aangeroepen door brondata.dms                              //
//       In deze configuratiefile wordt de sloop geconfigureerd                         //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

container Sloop
{
 	container WoningResterend :=
 		for_each_ndva(Nieuwbouw/ZW/hasNieuwbouw ? Nieuwbouw/ZW/name : ''
 			, Geography/rdc_grids/m100, float32
 			, SpecifiekeInstellingen/WloScenarioFolder+'/'+Nieuwbouw/ZW/periode_name+'/Wonen/Resterend/'
 			+ Nieuwbouw/ZW/wonen_wlo_filename+ '.tif'
 		);

	container WoningResterendFactors :=
		for_each_nedv(  Nieuwbouw/ZW/hasNieuwbouw ? Nieuwbouw/ZW/name : ''
			, 'median(0.0, float64(WoningResterend/'+Nieuwbouw/ZW/name +'), '
				+ (Nieuwbouw/ZW/Periode_Eindjaar > 2030w ? Nieuwbouw/ZW/prev_name : '1.0')+')' 
			, Geography/rdc_grids/m100, float64
		);

 	container UtilDeltaFactor :=
 		for_each_ndva(  Nieuwbouw/ZU/hasNieuwbouw && strlen(Nieuwbouw/ZU/wlo_filename_Nieuwbouw) > 0  ? Nieuwbouw/ZU/name : ''
 			, Geography/rdc_grids/m100, float32
 			, SpecifiekeInstellingen/WloScenarioFolder+'/'+Nieuwbouw/ZU/periode_name+'/Util/Resterend/'
 			+ Nieuwbouw/ZU/wlo_filename_Nieuwbouw + '.tif'
 		);

	container UtilResterendFactors :=
		for_each_nedv(  Nieuwbouw/ZU/hasNieuwbouw ? Nieuwbouw/ZU/name : ''
			, Nieuwbouw/ZU/Periode_Eindjaar > 2020w && strlen(Nieuwbouw/ZU/wlo_filename_Nieuwbouw) > 0  
				? 'median(0.0, float64(UtilDeltaFactor/'+Nieuwbouw/ZU/name +'), '
					+ (Nieuwbouw/ZU/Periode_Eindjaar > 2030w ? Nieuwbouw/ZU/prev_name : '1.0')+')' 
				: 'const(1.0, Geography/rdc_grids/m100)'
			, Geography/rdc_grids/m100, float64
		);

	container Totalen
	{
		attribute<float32> WoningResterend     (Nieuwbouw/ZW) := ='union_data(Nieuwbouw/ZW,'+AsItemList(Nieuwbouw/ZW/hasNieuwbouw ? 'sum(Sloop/WoningResterend/' + Nieuwbouw/ZW/name + ')' : '0f / 0f')+')';
		attribute<string>  YearTypeW           (Nieuwbouw/ZW) := Nieuwbouw/ZW/name;
		attribute<float32> UtilDeltaFactorSum  (Nieuwbouw/ZU) := ='union_data(Nieuwbouw/ZU,'+AsItemList(Nieuwbouw/ZU/hasNieuwbouw && strlen(Nieuwbouw/ZU/wlo_filename_Nieuwbouw) > 0 ?  'sum(UtilDeltaFactor/' + Nieuwbouw/ZU/name + ')' : '0f / 0f')+')';
		attribute<float32> UtilDeltaFactorMean (Nieuwbouw/ZU) := ='union_data(Nieuwbouw/ZU,'+AsItemList(Nieuwbouw/ZU/hasNieuwbouw && strlen(Nieuwbouw/ZU/wlo_filename_Nieuwbouw) > 0 ? 'mean(UtilDeltaFactor/' + Nieuwbouw/ZU/name + ')' : '0f / 0f')+')';
		attribute<string>  YearTypeU           (Nieuwbouw/ZU) := Nieuwbouw/ZU/name;
	}
}
