//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) VESTA 2020 - Planbureau voor de Leefomgeving                   //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////


container NieuwbouwUtilOperaties
{
	template Select
	{
		// begin case parameters
		attribute<int32>     Data_att (Geography/rdc_grids/m100);
		parameter<string>    utiliteit_lbl;
		parameter<yr_uint16> ZichtJaar;
		// einde case parameters

		parameter<ratio> SpreidingRekenjaren := (1d / sum(float64(Classifications/ZichtJaar/jaar[Classifications/Rekenstap/ZichtJaar_rel] == ZichtJaar)))[ratio];
		attribute<m2> att (Geography/rdc_grids/m100) := Data_att[m2] * SpreidingRekenjaren;
		
		unit<uint32> BebouwingsObject := Subset(att > 0[m2] && IsDefined(Geography/rdc_grids/m100/buurt_Rel) && Invoer/RuimtelijkeData/StudieGebied/selected_per_m100)
		,	DialogType = "map"
		,	DialogData = "point"
		{
			attribute<Geography/rdc_meter>                  point             := Geography/rdc_grids/m100/point[nr_OrgEntity];
			attribute<nrAsl>                                nrAansluitingen_i := const(1, ., nrAsl);
			attribute<pand_asl>                             Pand_aandeel      := const(1.0[pand_asl], .);
			attribute<m2>                                   Oppervlakte_i     := att[nr_OrgEntity];
			attribute<Invoer/RuimtelijkeData/StudieGebied/buurt> buurt_rel    := Geography/rdc_grids/m100/buurt_rel[nr_OrgEntity];
			attribute<Classifications/UtilTypeNieuwbouw>    BebouwingsType    := const(rlookup(utiliteit_lbl, Classifications/UtilTypeNieuwbouw/label), .);
			attribute<Classifications/BouwjaarUtilBestaand> BouwjaarOrg       := const(classify(value(ZichtJaar, yr) - 0.5[yr], classifications/BouwjaarUtilBestaand/ClassBreak), .);
			attribute<Classifications/BouwjaarUtilBestaand> Bouwjaar          := max_elem(bouwjaarOrg, Classifications/BouwjaarUtilBestaand/V/v2030);

			container Gebied
			{
				attribute<float64> n      (..) := float64(NrAansluitingen_i);
				attribute<float64> MEAN_x (..) := float64(pointCol(point));
				attribute<float64> MEAN_y (..) := float64(pointRow(point));
				attribute<float64> SSD_xx (..) := const(10000.0 / 12.0, ..);
				attribute<float64> SSD_xy (..) := const(    0.0 / 12.0, ..);
				attribute<float64> SSD_yy (..) := const(10000.0 / 12.0, ..);
			}
			container Kapitaallasten := for_each_nedv(
				Classifications/Gebouw_Kapitaallasten/name,
				'const(0[Eur_yr], BebouwingsObject)',
				BebouwingsObject,
				Eur_yr);
		}
	}
}