//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) Hestia 2021 - PBL & TNO                                        //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

Template BerekenFunctioneel
{
	unit<uint32> BOdomain;
	unit<uint32> PrevState;
	attribute<PrevState> PrevState_rel (BOdomain);

	attribute<GJ_yr> VT (BOdomain) :=
		  Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes/V_E_opp[PrevState/InstallatiePerProduct/VT[PrevState_rel]]
		* PrevState/Oppervlakte_i[PrevState_rel]
		* PrevState/Functioneel_base/Gedragfactor/VT
		* PrevState/Functioneel_base/Fitfactor_VT[PrevState_rel];
		
	attribute<GJ_yr> KK (BOdomain) := PrevState/Functioneel_base/KK[PrevState_rel];
	attribute<GJ_yr> EA (BOdomain) := PrevState/Functioneel_base/EA[PrevState_rel];
	attribute<GJ_yr> TW (BOdomain) := PrevState/Functioneel_base/TW[PrevState_rel];
	attribute<GJ_yr> KD (BOdomain) := PrevState/Functioneel_base/KD[PrevState_rel];
	attribute<GJ_yr> RV (BOdomain) := RV_per_Bouwdeel_now/Totaal * Correction_RV - warmtewinst/totaal + Vrv_VT;
	
	attribute<GJ_yr> Vrv_VT (BOdomain) :=
		  Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes/V_RV_asl[PrevState/InstallatiePerProduct/VT[PrevState_rel]]
		* PrevState/nrAansluitingen[PrevState_rel]
		* PrevState/Functioneel_base/Klimaateffect[PrevState_rel]
		* PrevState/Functioneel_base/Gedragfactor/VT
		* PrevState/Functioneel_base/Fitfactor_VT[PrevState_rel];

	attribute<Kwh_yr_m2>                  Kwh_m2_RV      (BOdomain) := makedefined((RV_per_Bouwdeel_now/Totaal / 3.6[MJ / kWh] * 1000 [ MJ / GJ]) / PrevState/Oppervlakte[PrevState_rel],0.0[Kwh_yr_m2]);
	attribute<Kwh_yr_m2>                  Kwh_m2_tot     (BOdomain) := makedefined(((VT+KK+EA+TW/0.72+KD*0.0+RV/1.04) / 3.6[MJ / kWh] * 1000 [ MJ / GJ]) / PrevState/Oppervlakte[PrevState_rel],0.0[Kwh_yr_m2]), IntegrityCheck = "Kwh_m2_tot < 500[Kwh_yr_m2]";
	attribute<Classifications/Schillabel> Schillabel_rel (BOdomain) := classify(Kwh_m2_tot, classifications/Schillabel/classbreak);
	attribute<ratio>                      Correction_RV  (BOdomain) := 1.0[ratio] + Invoer/Kengetallen/Constanten/Rebound_A * Invoer/Kengetallen/Constanten/Rebound_B ^ Kwh_m2_RV;

	container IsolatieNiveau_rel := for_each_nedv(
		classifications/Bouwdeel/name,
		replace(
			'not(Bouwdelen/IsChangedNow/@BN@)'
			'? PrevState/Bouwdelen/Isolatie/@BN@[PrevState_rel]'
			': Bouwdelen/Isolatie/@BN@'
		,	'@BN@', classifications/Bouwdeel/name),
		BOdomain,
		Classifications/IsolatieNiveau);
		
	container BouwdeelIsolatie_rel := for_each_nedv(
		classifications/Bouwdeel/name,
		replace(
			'combine_data(Classifications/BouwdeelIsolatie, classifications/Bouwdeel/V/@BN@, IsolatieNiveau_rel/@BN@)'
		,	'@BN@', classifications/Bouwdeel/name),
		BOdomain,
		Classifications/BouwdeelIsolatie);

	attribute<ratio> R_RV (Classifications/BouwdeelIsolatie):= Classifications/BouwdeelIsolatie/R_RV / 100[percent];
	
	container RV_per_Bouwdeel_now := for_each_nedv(
		classifications/Bouwdeel/name,
		replace(
			'PrevState/Functioneel_base/RV_N1_per_Bouwdeel_now/@BN@[PrevState_rel] * makedefined(1.0[ratio] - R_RV[BouwdeelIsolatie_rel/@BN@], 1.0[ratio])'
		,	'@BN@', classifications/Bouwdeel/name),
		BOdomain,
		GJ_yr)
	{
		attribute<GJ_yr> Totaal (BOdomain):= ='add('+AsItemList(Classifications/bouwdeel/name)+')';
	}

	container warmtewinst
	{
		//============ warmtewinst door personen wordt bepaald als constante
		attribute<GJ_yr> warmtewinst_personen         (BOdomain) := const(2.18[GJ_yr],...);

		//============ warmtewinst door apparaten wordt gelijkgesteld aan de vraag naar elektriciteit voor elektrische apparatuur
		attribute<GJ_yr> warmtewinst_apparaten        (BOdomain) := Functioneel/EA;

		//============ warmtewist door zoninstralen door ramen boven (RB) en onder (RO) als functie van oppervlak bouwdeel en waarde horende bij isolatiegraad
		attribute<GJ_yr> warmtewinst_zoninstraling_RB (BOdomain) := bouwdelen/RB * merge(uint8(bouwdelen/isolatie/RB), GJ_yr_m2, 0.456988[GJ_yr_m2],0.403224[GJ_yr_m2],0.322580[GJ_yr_m2],0.215053[GJ_yr_m2]), descr = "warmtewinst ramen boven";
		attribute<GJ_yr> warmtewinst_zoninstraling_RO (BOdomain) := bouwdelen/RO * merge(uint8(bouwdelen/isolatie/RO), GJ_yr_m2, 0.456988[GJ_yr_m2],0.403224[GJ_yr_m2],0.322580[GJ_yr_m2],0.215053[GJ_yr_m2]), descr = "warmtewinst ramen onder";
		attribute<GJ_yr> warmtewinst_zoninstraling    (BOdomain) := warmtewinst_zoninstraling_RO + warmtewinst_zoninstraling_RB;	

		//============ totaal warmtewinsten
		attribute<GJ_yr> Totaal                       (BOdomain) := warmtewinst_personen + warmtewinst_apparaten + warmtewinst_zoninstraling;
	}

}