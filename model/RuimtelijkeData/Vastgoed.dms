//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) Hestia 2021 - Planbureau voor de Leefomgeving                  //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//       In deze configuratiefile worden de brondata geconfigureerd.                    //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

container Vastgoed
{
	parameter<string> Scenario := 'WLO_hoog_DichtBij'; //Keuze uit:
		//WLO_hoog_DichtBij, WLO_hoog_EnZoVoort, WLO_hoog_Ruim, WLO_hoog_Verbonden
		//WLO_laag_DichtBij, WLO_laag_EnZoVoort, WLO_laag_Ruim, WLO_laag_Verbonden
		
	unit<uint8> Periode: nrofrows = 3
	{
		attribute<string>          name         : ['J2021_J2030', 'J2031_J2040', 'J2041_J2050'], DialogType = "LabelText";
		attribute<units/yr_uint16> StartPeriode : [2021, 2031, 2041];
		attribute<units/yr_uint16> EindePeriode : [2030, 2040, 2050]; 
		attribute<string>          directory    : ['J21_J50', 'J21_J50', 'J21_J50']; // in SD/Vastgoed/[x]
	}
	unit<uint8> VastgoedFiles: nrofrows = 2
	{
		attribute<string> name     : [ 'Bouw', 'Sloop'];
	}
	
	container VastgoedPeriode := for_each_ne(
		 Periode/name
		,'LeesVastgoed(
			  '+ quote(Periode/directory) +'
			, '+ string(Periode/StartPeriode) +'[units/yr_uint16]
			, '+ string(Periode/EindePeriode) +'[units/yr_uint16]
			, Scenario)
			');
	
	template LeesVastgoed
	{
		//case parameters
		parameter<string>          PeriodeNaam;
		parameter<units/yr_uint16> StartPeriode;
		parameter<units/yr_uint16> EindePeriode;
		parameter<string>          ScenarioNaam;
		//end case parameters
		
		container Data :=
			for_each_ndva(
				 VastgoedFiles/name
				,Geography/rdc_grids/m100
				,float32
				,'%SourceDataProjDir%/Vastgoed/'+ PeriodeNaam +'/' + VastgoedFiles/name + '/' + ScenarioNaam + '_Wonen_totaal' + '.tif'
			)
		{
			attribute<float32> BouwPerJaar  (Geography/rdc_grids/m100) := Bouw  / float32(EindePeriode - StartPeriode);
			attribute<float32> SloopPerJaar (Geography/rdc_grids/m100) := Sloop / float32(EindePeriode - StartPeriode);
		}
	}
	
	unit<uint8> BouwKaarten := Classifications/Rekenstap
	{
		attribute<units/yr_uint16> Jaar        (.) := /Classifications/Rekenstap/jaar;
		attribute<string>          name        (.) := 'B_' + string(Jaar);
		attribute<periode>         Periode_rel (.) := classify(Jaar, Periode/StartPeriode);
		
		container Nieuwbouw :=
			for_each_nedv(name, isnull(Periode_rel) ? 'const(0f,Geography/rdc_grids/m100)' : 'VastgoedPeriode/' +Periode/name[periode_rel]+ '/Data/BouwPerJaar' ,Geography/rdc_grids/m100 ,float32 );
			
		container Sloop :=
			for_each_nedv(name, isnull(Periode_rel) ? 'const(0f,Geography/rdc_grids/m100)' : 'VastgoedPeriode/' +Periode/name[periode_rel]+ '/Data/SloopPerJaar' ,Geography/rdc_grids/m100 ,float32 );
		
	}			
}