//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) Hestia 2021 - PBL & TNO                                        //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//       Deze DMS-file wordt aangeroepen door stam.dms                                  //
//       In deze configuratiefile wordt de brondata huidige Warmtenetten geconfigureerd //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

container BestaandeWarmtenetten
{
	unit<uint32> Aflevergebied_data
	:   StorageName     = "%HestiaDataDir%/infra/BestaandeWarmtenetten/aflevergebied.shp"
	,   StorageType     = "gdal.vect"
	,	DialogData      = "geometry"
	,	DialogType      = "Map"
	,	StorageReadOnly = "True"
	{
	   attribute<rdc_meter> geometry   (poly);
	   attribute<rdc_mm   > geometry_mm(poly) := geometry[rdc_mm];

	   attribute<string>      label := Eigenaar + ' ' + Warmtenet;
		
		attribute<yr_uint16>   StartJaar;
		attribute<string>      Warmtenet;
		attribute<string>      Eigenaar;
		attribute<string>      Type;
		attribute<yr_uint16>   Jaar;
		attribute<string>      Nauwkeurig;
		attribute<Leverancier>  Leverancier_rel := rlookup(Eigenaar, Leverancier/name);
		attribute<AfleverType>  Type_rel := rlookup(Type, AfleverType/name);
		attribute<Warmtenetten> Warmtenet_rel   := rlookup(Warmtenet, Warmtenetten/name);
	}
	
	unit<uint32> overlap := polygon_connectivity(Aflevergebied_data/geometry[rdc_mm])
	{
		attribute<rdc_mm>    geometry_mm(poly) := Aflevergebied_data/Geometry_mm[F1] * Aflevergebied_data/Geometry_mm[F2];
		attribute<rdc_meter> geometry   (poly) := geometry_mm[rdc_meter];
		attribute<m2>        area := area(geometry, m2);
	}
	
	unit<uint32> AfleverType := unique(Aflevergebied_data/Type), DisableStorage = "True"
	{
	   attribute<string> name  := Values;
	   attribute<string> label := name;
	   attribute<bool>   HeeftKoude := strpos(name, 'WKO') >= 0 || strpos(name, 'Koude') >= 0;
	}
	unit<uint32> Leverancier := unique(Aflevergebied_data/Eigenaar), DisableStorage = "True"
	{
	   attribute<string> name  := Values;
	   attribute<string> label := name;
	}
	unit<uint32> Warmtenetten := unique(Aflevergebied_data/Warmtenet), DisableStorage = "True"
	{
	   attribute<string> name  := Values;
	   attribute<string> label := name;

	   attribute<float64> nr_HWK := sum(float64(Puntbronnen_data/TypeInfoList_rel == TypeInfoList/V/HulpKetel), Puntbronnen_data/Warmtenet_rel), Descr = "aantal Hulpketels in warmtenet";
	   attribute<ratio>   RP_HWK := makedefined(0.85[Ratio] / nr_HWK, 0.0[ratio]), Descr = "Aandeel vermogen per hulpketel";

	   attribute<Eur_kW>  Ki_kw_min := sum(Puntbronnen_data/ki_kw_max * Puntbronnen_data/P_cap, Puntbronnen_data/Warmtenet_rel);
	   attribute<Eur_kW>  Ki_kw_max := sum(Puntbronnen_data/ki_kw_min * Puntbronnen_data/P_cap, Puntbronnen_data/Warmtenet_rel);
	   attribute<Celsius> T_bron    := max(Puntbronnen_data/Temp_bron, Puntbronnen_data/Warmtenet_rel);
	   attribute<_yr>     R_OH      := sum(Puntbronnen_data/R_OH * Puntbronnen_data/P_cap, Puntbronnen_data/Warmtenet_rel);

	   container K_GJ := for_each_nedv(classifications/Zichtjaar/name
			, replace('sum(Puntbronnen_data/K_GJ/@X * Puntbronnen_data/P_vol_r, Puntbronnen_data/Warmtenet_rel)', '@X', classifications/Zichtjaar/name)
			, .
			, Eur_GJ
		);
		container Km_GJ := for_each_nedv(classifications/Zichtjaar/name
			, replace('sum(Puntbronnen_data/Km_GJ/@X * Puntbronnen_data/P_vol_r, Puntbronnen_data/Warmtenet_rel)', '@X', classifications/Zichtjaar/name)
			, .
			, Eur_GJ
		);
	}


	unit<uint32> TypeInfoList := Invoer/Kengetallen/WarmteBronnen/MetaData/TypeInfoList;
	unit<uint32> TypeInfo     := Invoer/Kengetallen/WarmteBronnen/MetaData/TypeInfo;

	unit<uint32> Puntbronnen_data
	:   StorageName     = "%HestiaDataDir%/infra/BestaandeWarmtenetten/puntbronnen.shp"
	,   StorageType     = "gdal.vect"
	,	DialogData      = "geometry"
	,	DialogType      = "Map"
	,	StorageReadOnly = "True"
	{
		
	   attribute<rdc_meter> geometry   ;
	   attribute<rdc_mm   > geometry_mm := geometry[rdc_mm];

	   attribute<string>      label := Eigenaar + ' ' + Warmtenet;
	   attribute<percent>     P_vol; 
	   attribute<ratio>       P_vol_r := P_vol / 100[percent];

	   attribute<TypeInfo>     TypeInfo_rel     := rlookup(TypeInfoList_rel, TypeInfo/TypeInfoList_rel);
	   attribute<TypeInfoList> TypeInfoList_rel := switch(
	   		  case(Type == 'AVI'       , TypeInfoList/V/AVI)
	   		, case(Type == 'STEG'      , TypeInfoList/V/STEG)
	   		, case(Type == 'BMC'       , TypeInfoList/V/BMC)
	   		, case(Type == 'BioWKK'    , TypeInfoList/V/BioWKK)
	   		, case(Type == 'Gasketel'  , TypeInfoList/V/HulpKetel)
	   		, case(Type == 'Hulpketel' , TypeInfoList/V/HulpKetel)
	   		, case(Type == 'Gasturbine', TypeInfoList/V/Gasturbine)
	   		, case(Type == 'Geothermie', TypeInfoList/V/Geothermie)
	   		, case(Type == 'WKK'       , TypeInfoList/V/WijkWKK)
	   		, case(Type == 'Gasmotor'  , TypeInfoList/V/Gasmotor)
	   		, TypeInfoList/V/Industrie);

		attribute<Eur_kW>  Ki_kW_min := TypeInfo/Ki_KW_min[TypeInfo_rel];
		attribute<Eur_kW>  Ki_kW_max := TypeInfo/Ki_KW_max[TypeInfo_rel];
		attribute<Celsius> Temp_bron := TypeInfo/Temp_default[TypeInfo_rel];
		attribute<ratio>   P_cap     := not(TypeInfoList_rel == TypeInfoList/V/HulpKetel) ? TypeInfo/Pcap_default[TypeInfo_rel] : Warmtenetten/RP_HWK[Warmtenet_rel];
		attribute<_yr>     R_OH      := TypeInfo/JaarKosten[_yr][TypeInfo_rel];

		container K_GJ := for_each_nedv(classifications/Zichtjaar/name
			, replace('TypeInfo/K_GJ/@X[TypeInfo_rel]', '@X', classifications/Zichtjaar/name)
			, .
			, Eur_GJ
		);
		container Km_GJ := for_each_nedv(classifications/Zichtjaar/name
			, replace('TypeInfo/Km_GJ/@X[TypeInfo_rel]', '@X', classifications/Zichtjaar/name)
			, .
			, Eur_GJ
		);

		attribute<Leverancier>  Leverancier_rel := rlookup(Eigenaar, Leverancier/name);
		attribute<Warmtenetten> Warmtenet_rel   := rlookup(Warmtenet, Warmtenetten/name);
	}
}