//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) Hestia 2021 - PBL & TNO                                        //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

template AllocatieResultatenComponent
{
	// begin case parameters
	unit<uint32>      BO_base;
	parameter<string> BebouwingsComponent_name;
	// end case parameters

	container    VraagKentallen := BO/VraagKentallen, IsHidden = "True";
	unit<uint32> PlanRegio      := Invoer/SpecifiekeInstellingen/PlanRegio, IsHidden = "True";

	attribute<Classifications/WoningType>  BebouwingsType          (BO) := BO/BebouwingsType, IsHidden = "True";
	attribute<bool>                        VeroorzaaktGrondroering (BO) := = 'OR('+AsItemList(replace('BO/InstallatiePerProduct/@PD@ == Classifications/Installatie/V/gebiedsOptie', '@PD@', Classifications/Product/name))+')', IsHidden = "True";

	attribute<bool>                        GrondroeringInPlanRegio (PlanRegio) := sum(VeroorzaaktGrondroering ? 1 : 0, BO/PlanRegio_rel) > 0, IsHidden = "True";

	unit<uint32> BO := BO_base
	{
		attribute<Classifications/Schillabel>  Schillabel_rel   := BO_base/Schillabel_rel;
		attribute<Classifications/Schillabel>  Energielabel_rel   := BO_base/Energielabel_rel;

        
		attribute<bool> KoudeGebouw := (IsDefined(BemeterdeGebouwInput_rel/KDb) || IsDefined(BemeterdeGebouwInput_rel/KDp)) && not(KoudeGebied);
		attribute<bool> KoudeGebied := BO/InstallatiePerProduct/Kdb == Classifications/Installatie/V/Gebiedsoptie || BO/InstallatiePerProduct/Kdp == Classifications/Installatie/V/Gebiedsoptie;
			
		container BemeterdeGebouwInput_rel :=  BO_base/BemeterdeGebouwInput_rel, IsHidden = "True";
			
		container Aansluitingen := BO_Base/Aansluitingen
		{		
			
			attribute<nrAsl> Enet_hwp (BO) := nrAansluitingen * float64(BemeterdeGebouwInput_rel/RVb == Classifications/BemeterdeGebouwInput/V/e	&& BemeterdeGebouwInput_rel/RVp != Classifications/BemeterdeGebouwInput/V/e);
			attribute<nrAsl> Enet_app (BO) := nrAansluitingen * float64((Enet_hwp + Enet_ewp) == 0[nrAsl]), descr = "gebouw heeft alleen een lektriciteitsaansluiting voor overige apparatuur";
			attribute<nrAsl> Enet_ewp (BO) := nrAansluitingen * float64(
				and(   BemeterdeGebouwInput_rel/RVb == Classifications/BemeterdeGebouwInput/V/e
					 , BemeterdeGebouwInput_rel/RVp == Classifications/BemeterdeGebouwInput/V/e
					 , BemeterdeGebouwInput_rel/TWb == Classifications/BemeterdeGebouwInput/V/e
					 , BemeterdeGebouwInput_rel/TWp == Classifications/BemeterdeGebouwInput/V/e ));
		}

		container Functioneel := BO_Base/Functioneel
		{
			attribute<GJ_yr> V_RV      (BO) := BO_Base/Functioneel/RV;
			attribute<GJ_yr> V_TW      (BO) := BO_Base/Functioneel/TW;
			attribute<GJ_yr> V_Warmte  (BO) := V_RV + V_TW;
			attribute<GJ_yr> V_KD      (BO) := BO_Base/Functioneel/KD;
			attribute<GJ_yr> V_KK      (BO) := BO_Base/Functioneel/KK;
			attribute<GJ_yr> V_VT      (BO) := BO_Base/Functioneel/VT;
		
			attribute<GJ_yr> EA        (BO) := BO_base/Functioneel/EA;
			attribute<GJ_yr> Totaal_LO (BO) :=    V_RV * float64(Aansluitingen/Warmte == 0.0[nrAsl]) + V_TW * float64(Aansluitingen/Warmte == 0.0[nrAsl])
												+ V_VT + V_KD * float64(KoudeGebouw);
		}

		container Func2Meter := CalculationSchemes/FunctioneelToMetervraag ( BO_Base , false);
		
		container Uitdraai_EDW
	    {
        unit<uint32> table := select_with_org_rel(isdefined(BO/code))
		{attribute<String> vbo_id						:= BO/code[org_rel];
		attribute<UInt32> eigendomstype					:= BO/Eigendom_rel[org_rel];
		attribute<Uint8> woningtype						:= BO/BebouwingsType[org_rel]; 
		attribute<UInt16> bouwjaar						:= BO/bouwjaar[org_rel];
		attribute<UInt16> sloopjaar						:= BO/sloopjaar[org_rel];
		attribute<UInt8> nr_verdiepingen				:= BO/nr_verdiepingen[org_rel];
		attribute<m2> gebruiksoppervlak					:= BO/oppervlakte[org_rel];
		attribute<m2> footprint							:= BO/footprint[org_rel];
		attribute<string> adres							:= BO/adres[org_rel];
		attribute<string> BU_NAAM						:= BO/buurtnaam[org_rel];
		attribute<string> BU_CODE						:= BO/BU_code[org_rel];
		attribute<float64> Buurt_area					:= BO/Buurt_area[org_rel];
		attribute<string> WK_CODE						:= BO/WK_CODE[org_rel];
		attribute<string> Gemeente_name					:= BO/Gemeente_name[org_rel];
		attribute<string> GM_code						:= BO/GM_code[org_rel];
		attribute<string> PROV_NAAM						:= BO/PROV_NAAM[org_rel];
		attribute<string> res_regio						:= BO/res_regio[org_rel];
				//attribute<string> heeft_gas       						:= Woning/Metervraag/Aardgas[org_rel] > 0[GJ_yr] || Woning/Metervraag/Waterstof[org_rel] > 0[GJ_yr] ? 'ja' : 'nee';

		attribute<m2> vloer_opp									:= BO/Bouwdelen/VL [org_rel];
		attribute<m2> schuin_dak_opp							:= BO/Bouwdelen/DS [org_rel];
		attribute<m2> plat_dak_opp								:= BO/Bouwdelen/DP [org_rel];
		attribute<m2> massieve_gevel_opp						:= BO/Bouwdelen/MG [org_rel];
		attribute<m2> spouwmuur_opp								:= BO/Bouwdelen/MS [org_rel];
		attribute<m2> paneel_opp								:= BO/Bouwdelen/PL [org_rel];
		attribute<m2> raam_boven_opp							:= BO/Bouwdelen/RB [org_rel];
		attribute<m2> raam_onder_opp							:= BO/Bouwdelen/RO [org_rel];
		attribute<m2> deur_opp									:= BO/Bouwdelen/DR [org_rel];
		attribute<m2> schill_opp								:= vloer_opp + schuin_dak_opp + plat_dak_opp + massieve_gevel_opp + spouwmuur_opp + paneel_opp + raam_boven_opp + raam_onder_opp + deur_opp;
	
		attribute<Classifications/IsolatieNiveau> schuin_dak_isolatie		:= BO/Bouwdelen/Isolatie/DS[org_rel];
		attribute<Classifications/IsolatieNiveau> plat_dak_isolatie			:= BO/Bouwdelen/Isolatie/DP[org_rel];
		attribute<Classifications/IsolatieNiveau> massieve_gevel_isolatie	:= BO/Bouwdelen/Isolatie/MG[org_rel];
		attribute<Classifications/IsolatieNiveau> spouwmuur_isolatie		:= BO/Bouwdelen/Isolatie/MS[org_rel];
		attribute<Classifications/IsolatieNiveau> vloer_isolatie			:= BO/Bouwdelen/Isolatie/VL[org_rel];
		attribute<Classifications/IsolatieNiveau> paneel_isolatie			:= BO/Bouwdelen/Isolatie/PL[org_rel];
		attribute<Classifications/IsolatieNiveau> raam_boven_isolatie		:= BO/Bouwdelen/Isolatie/RB [org_rel];
		attribute<Classifications/IsolatieNiveau> raam_onder_isolatie		:= BO/Bouwdelen/Isolatie/RO[org_rel];
		attribute<Classifications/IsolatieNiveau> deur_isolatie				:= BO/Bouwdelen/Isolatie/DR[org_rel];
		attribute<Classifications/IsolatieNiveau> kier_isolatie	    		:= BO/Bouwdelen/Isolatie/KR[org_rel];

		attribute<Classifications/Installatie> installatie_rv_basis     								:= BO/InstallatiePerProduct/RVb[org_rel]; 
		attribute<Classifications/Installatie> installatie_rv_piek      								:= BO/InstallatiePerProduct/RVp[org_rel]; 
		attribute<Classifications/Installatie> installatie_tw_basis     								:= BO/InstallatiePerProduct/TWb[org_rel]; 
		attribute<Classifications/Installatie> installatie_tw_piek      								:= BO/InstallatiePerProduct/TWp[org_rel]; 
		attribute<Invoer/Kengetallen/OverigeInstallaties/KK/Installatietypes>  installatie_koken  		:= BO/InstallatiePerProduct/KK[org_rel]; 
		attribute<Classifications/Installatie> installatie_kd_basis     								:= BO/InstallatiePerProduct/KDb[org_rel]; 
		attribute<Classifications/Installatie> installatie_kd_piek      								:= BO/InstallatiePerProduct/KDp[org_rel]; 
		attribute<Invoer/Kengetallen/OverigeInstallaties/VT/Installatietypes>  installatie_ventilatie	:= BO/InstallatiePerProduct/VT[org_rel]; 
		attribute<Invoer/Kengetallen/OverigeInstallaties/DK/Installatietypes>  installatie_dak          := BO/InstallatiePerProduct/DK[org_rel]; 

		attribute<Classifications/Schillabel> energielabel_afgemeld						:= BO/Schillabel_rel[org_rel];
		attribute<Classifications/Schillabel> energielabel_default						:= BO/Default_Energielabel_rel[org_rel];
		attribute<Classifications/Schillabel> energielabel_berekend						:= BebouwingsComponenten/Woning/BO/EnergielabelBerekening/Energielabel_rel[org_rel];		

		attribute<GJ_yr> functionele_vraag_rv			:= Functioneel/V_RV[org_rel];
		attribute<GJ_yr> functionele_vraag_tw			:= Functioneel/V_TW[org_rel];
		attribute<GJ_yr> functionele_vraag_koken		:= Functioneel/V_KK[org_rel];
		attribute<GJ_yr> functionele_vraag_kd			:= Functioneel/V_KD[org_rel];
		attribute<GJ_yr> functionele_vraag_ea			:= Functioneel/EA[org_rel];
		
		attribute<GJ_yr> metervraag_gas       					:= Woning/Metervraag/Gas[org_rel];
		attribute<GJ_yr> metervraag_elektriciteit				:= Woning/Metervraag/E[org_rel];
		attribute<GJ_yr> metervraag_warmte						:= Woning/Metervraag/warmte_30[org_rel] + Woning/Metervraag/warmte_50[org_rel] + Woning/Metervraag/warmte_70[org_rel];
		attribute<GJ_yr> metervraag_biomassa       				:= Woning/Metervraag/Biomassa[org_rel];
		attribute<GJ_yr> metervraag_olie       					:= Woning/Metervraag/Olie[org_rel];
		
		attribute<GJ_yr> opbrengst_zon_pv      					:= Woning/Metervraag/Oj_e_ZonPV[org_rel];

		attribute<GJ_yr> metervraag_gas_rv_basis           		:= Woning/Metervraag/metervraag_gas_rv_basis[org_rel];
		attribute<GJ_yr> metervraag_gas_rv_piek            		:= Woning/Metervraag/metervraag_gas_rv_piek[org_rel];
		attribute<GJ_yr> metervraag_elektriciteit_rv_basis  	:= Woning/Metervraag/metervraag_elektriciteit_rv_basis[org_rel];
		attribute<GJ_yr> metervraag_elektriciteit_rv_piek   	:= Woning/Metervraag/metervraag_elektriciteit_rv_piek[org_rel];
		attribute<GJ_yr> metervraag_warmte_rv_basis         	:= Woning/Metervraag/metervraag_warmte_rv_basis[org_rel];
		attribute<GJ_yr> metervraag_warmte_rv_piek          	:= Woning/Metervraag/metervraag_warmte_rv_piek[org_rel];
		attribute<GJ_yr> metervraag_biomassa_rv_basis       	:= Woning/Metervraag/metervraag_biomassa_rv_basis[org_rel];
		attribute<GJ_yr> metervraag_biomassa_rv_piek        	:= Woning/Metervraag/metervraag_biomassa_rv_piek[org_rel];
		attribute<GJ_yr> metervraag_olie_rv_basis            	:= Woning/Metervraag/metervraag_olie_rv_basis[org_rel];
		attribute<GJ_yr> metervraag_olie_rv_piek             	:= Woning/Metervraag/metervraag_olie_rv_piek[org_rel];
		attribute<GJ_yr> metervraag_gas_tw_basis            	:= Woning/Metervraag/metervraag_gas_tw_basis[org_rel];
		attribute<GJ_yr> metervraag_gas_tw_piek             	:= Woning/Metervraag/metervraag_gas_tw_piek[org_rel];
		attribute<GJ_yr> metervraag_elektriciteit_tw_basis  	:= Woning/Metervraag/metervraag_elektriciteit_tw_basis[org_rel];
		attribute<GJ_yr> metervraag_elektriciteit_tw_piek   	:= Woning/Metervraag/metervraag_elektriciteit_tw_piek[org_rel];
		attribute<GJ_yr> metervraag_warmte_tw_basis         	:= Woning/Metervraag/metervraag_warmte_tw_basis[org_rel];
		attribute<GJ_yr> metervraag_warmte_tw_piek          	:= Woning/Metervraag/metervraag_warmte_tw_piek[org_rel];
		attribute<GJ_yr> metervraag_biomassa_tw_basis       	:= Woning/Metervraag/metervraag_biomassa_tw_basis[org_rel];
		attribute<GJ_yr> metervraag_biomassa_tw_piek        	:= Woning/Metervraag/metervraag_biomassa_tw_piek[org_rel];
		attribute<GJ_yr> metervraag_olie_tw_basis           	:= Woning/Metervraag/metervraag_olie_tw_basis[org_rel];
		attribute<GJ_yr> metervraag_olie_tw_piek            	:= Woning/Metervraag/metervraag_olie_tw_piek[org_rel];
		attribute<GJ_yr> metervraag_gas_koken               	:= Woning/Metervraag/metervraag_gas_koken[org_rel];
		attribute<GJ_yr> metervraag_elektriciteit_koken     	:= Woning/Metervraag/Vj_e_koken[org_rel];
		attribute<GJ_yr> metervraag_elektriciteit_kd_basis  	:= Woning/Metervraag/metervraag_elektriciteit_kd_basis[org_rel];
		attribute<GJ_yr> metervraag_elektriciteit_kd_piek   	:= Woning/Metervraag/metervraag_elektriciteit_kd_piek[org_rel];
		attribute<GJ_yr> metervraag_elektriciteit_ventilatie 	:= Woning/Metervraag/Vj_e_VT[org_rel];
		attribute<GJ_yr> metervraag_elektriciteit_ea        	:= Woning/Metervraag/Vj_e_EA[org_rel];


		attribute<Eur_GJ> kosten_gas_variabel      			:= BO/jaarlijks/kosten_gas_variabel[org_rel];
		attribute<Eur_yr> kosten_gas_vast	    			:= BO/jaarlijks/Kj_vastrecht_G[org_rel];
		attribute<Eur_GJ> kosten_elektriciteit_variabel     := BO/jaarlijks/kosten_elektriciteit_variabel[org_rel];
		attribute<Eur_yr> kosten_elektriciteit_vast    		:= BO/jaarlijks/Kj_vastrecht_E[org_rel];
		

		
		} 
		parameter<string> OutputFolder := '%localDataProjDir%/Results/IsolatieStandaard/';
        unit<uint32> variable := SubItem_PropValues(table,'name');
        container export_csv := Model/CalculationSchemes/Rapportage/WriteTable32ToCSV(table, AsList(variable/name, ';'), OutputFolder+rekenstapname+'IsolatieStandaard_export.csv');
        }
		
		container Metervraag := BO_Base/Metervraag
		{
			attribute<GJ_yr> EA        (BO) := Functioneel/EA / /Invoer/Kengetallen/Constanten/Efficiency_EA;
			attribute<GJ_yr> Totaal_LO (BO) := e + gas + pellets + biomassa + olie;
		}
		container Omgeving
		{
			attribute<GJ_yr> Winst   (BO) := max_elem(0.0[GJ_yr], Functioneel/Totaal_LO - Metervraag/Totaal_LO);
			attribute<GJ_yr> Verlies (BO) := max_elem(0.0[GJ_yr], Metervraag/Totaal_LO  - Functioneel/Totaal_LO);
		}
		container Opbrengsten
		{
			attribute<float64>    AansluitTarief     (BO) := BO_base/Opbrengsten/AansluitTarief;

			parameter<Eur_yr_asl> VastRechtMT             := BO_base/Opbrengsten/VastRechtMT;
			parameter<Eur_yr_asl> VastRechtLT             := BO_base/Opbrengsten/VastRechtLT;
			parameter<Eur_yr_asl> VastRechtK              := BO_base/Opbrengsten/VastRechtK;

			attribute<Eur>        AansluitBijdrageG  (BO) := BO_base/Opbrengsten/AansluitBijdrageG;
			attribute<Eur>        AansluitBijdrageE  (BO) := BO_base/Opbrengsten/AansluitBijdrageE;
			attribute<Eur>        AansluitBijdrageT  (BO) := BO_base/Opbrengsten/AansluitBijdrageT , Descr = "Aansluitbijdrage voor nieuwe aansluiting op warmtenetten, niet gecorrigeerd voor deelname";

			attribute<Eur_yr>     VastRechtBijdrageG (BO) := BO_base/Opbrengsten/VastRechtBijdrageG, Descr = "VastRechtBijdrage voor aansluiting op het gasnet";
			attribute<Eur_yr>     VastRechtBijdrageE (BO) := BO_base/Opbrengsten/VastRechtBijdrageE, Descr = "VastRechtBijdrage voor aansluiting op het elektriciteitsnetnet";

			attribute<Eur_yr>     VastRechtMT_T      (BO) := BO_base/Opbrengsten/VastRechtMT_T     , Descr = "VastRechtBijdrage voor aansluiting op warmtenetten, niet gecorrigeerd voor deelname";
			attribute<Eur_yr>     VastRechtLT_T      (BO) := BO_base/Opbrengsten/VastRechtLT_T     , Descr = "VastRechtBijdrage voor aansluiting op warmtenetten, niet gecorrigeerd voor deelname";
			attribute<Eur_yr>     VastRechtK_T       (BO) := BO_base/Opbrengsten/VastRechtK_T      , Descr = "VastRechtBijdrage voor aansluiting op warmtenetten, niet gecorrigeerd voor deelname";

			attribute<Eur_yr>     HeffingskortingE   (BO) := BO_base/Opbrengsten/HeffingskortingE;
		}

		container eenmalig := for_each_nedv(
			Classifications/GebouwOptie_eenmalig/name,
			'BO_base/eenmalig/'+Classifications/GebouwOptie_eenmalig/name,
			BO,
			Eur)
		{
			//Aansluitbijdrages
			attribute<Eur> Ki_Asl_Enet       (BO) := BC_kentallen/Ki_Aansl_e_asl * nraansluitingen + BC_kentallen/Ki_Aansl_e_opp * oppervlakte;
			attribute<Eur> Ki_Asl_Gnet       (BO) := Metervraag/gas <= 0[GJ_yr] ? 0[Eur] : BC_kentallen/Ki_Aansl_g_asl * nraansluitingen + BC_kentallen/Ki_Aansl_g_opp * oppervlakte;
			
			attribute<Eur> Ki_Asl_W70        (BO) := float64(Aansluitingen/Warmte > 0.0[nrAsl]) * Opbrengsten/AansluitBijdrageT;
			attribute<Eur> Ki_Asl_W50        (BO) := const(0.0[eur],BO);
			attribute<Eur> Ki_Asl_W30        (BO) := const(0.0[eur],BO);
			attribute<Eur> Ki_Asl_WK70       (BO) := const(0.0[eur],BO);
			attribute<Eur> Ki_Asl_WK50       (BO) := const(0.0[eur],BO);
			attribute<Eur> Ki_Asl_WK30       (BO) := const(0.0[eur],BO);
		}
		container Kapitaallasten := for_each_nedv(Classifications/Gebouw_kapitaallasten/name, replace('BO_Base/Kapitaallasten/@KL@', '@KL@', Classifications/Gebouw_kapitaallasten/name), BO, Eur_yr );

		container jaarlijks := BO_Base/Jaarlijks
		{
			//Vastrecht en korting
			attribute<Eur_yr> Kj_vastrecht_E (BO) := Opbrengsten/VastRechtBijdrageE;
			attribute<Eur_yr> Kj_vastrecht_G (BO) := Metervraag/gas <= 0.0[GJ_yr] ? 0.0[Eur_yr] : Opbrengsten/VastRechtBijdrageG;
			attribute<Eur_yr> Oj_Hkorting_E  (BO) := Opbrengsten/HeffingskortingE;

			//Elektriciteit
			attribute<classifications/gebruiksgrootteklasse> e_gebruiksgrootteklasse_rel (BO) := classify(Metervraag/e, Prijzen/Elektriciteit/Staffel/ClassBreak);
			attribute<Eur_yr> Kj_e           (BO) := Metervraag/e * Prijzen/Elektriciteit/Staffel/KGJ_eindgebruik_excl[e_gebruiksgrootteklasse_rel];
			attribute<Eur_GJ> kosten_elektriciteit_variabel        (BO) :=  Prijzen/Elektriciteit/Staffel/KGJ_eindgebruik_excl[g_gebruiksgrootteklasse_rel];

			attribute<Eur_yr> Km_e           (BO) := Metervraag/e * Prijzen/Elektriciteit/Staffel/KGJ_Maatschappelijk [e_gebruiksgrootteklasse_rel];
			attribute<Eur_yr> Kj_e_EH        (BO) := Metervraag/e * Prijzen/Elektriciteit/Staffel/KGJ_EnergieHeffing[e_gebruiksgrootteklasse_rel];

			//Gas
			attribute<classifications/gebruiksgrootteklasse> g_gebruiksgrootteklasse_rel (BO) := classify(Metervraag/gas, Prijzen/Aardgas/Staffel/ClassBreak);
			attribute<Eur_yr> Kj_gas         (BO) := Metervraag/gas * Prijzen/Aardgas/Staffel/KGJ_eindgebruik_excl[g_gebruiksgrootteklasse_rel];
			attribute<Eur_GJ> kosten_gas_variabel        (BO) :=  Prijzen/Aardgas/Staffel/KGJ_eindgebruik_excl[g_gebruiksgrootteklasse_rel];

			attribute<Eur_yr> Km_gas         (BO) := Metervraag/gas * Prijzen/Aardgas/Staffel/KGJ_Maatschappelijk [g_gebruiksgrootteklasse_rel];
			attribute<Eur_yr> Kj_gas_EH      (BO) := Metervraag/gas * Prijzen/Aardgas/Staffel/KGJ_EnergieHeffing[g_gebruiksgrootteklasse_rel];

			//Waterstof
			attribute<Eur_yr> Kj_H2          (BO) := Metervraag/H2 * prijzen/overig/Pj_H2;
			attribute<Eur_yr> Km_H2          (BO) := Metervraag/H2 * prijzen/overig/Pm_H2;
			attribute<Eur_yr> Kj_H2_EH       (BO) := const(0.0[eur_yr],BO);
		}
	}

	attribute<kw> CapaciteitsvraagE (PlanRegio) :=  sum(BO/AansluitCapaciteit/GTF_Enet *
		( BO/AansluitCapaciteit/ASW_Eapp * float64(BO/Aansluitingen/Enet_app > 0.0[nrAsl])
		+ BO/AansluitCapaciteit/ASW_Ehwp * float64(BO/Aansluitingen/Enet_hwp > 0.0[nrAsl])
		+ BO/AansluitCapaciteit/ASW_Eewp * float64(BO/Aansluitingen/Enet_ewp > 0.0[nrAsl]) ), BO/Planregio_rel);

	container Aansluitingen
	{
		attribute<nrAsl> gas      (PlanRegio) := sum(BO/Aansluitingen/Gnet,     BO/Planregio_rel);
		attribute<nrAsl> Enet_ewp (PlanRegio) := sum(BO/Aansluitingen/Enet_ewp * float64(not(BO/Aansluitingen/Warmte > 0[nrAsl])), BO/Planregio_rel);
		attribute<nrAsl> Enet_hwp (PlanRegio) := sum(BO/Aansluitingen/Enet_hwp, BO/Planregio_rel);
		attribute<nrAsl> LT_wp    (PlanRegio) := sum(BO/Aansluitingen/Enet_ewp * float64(BO/Aansluitingen/Warmte > 0[nrAsl])     , BO/Planregio_rel);
	}

	container Aandelen := for_each_nedv(Classifications/GebiedsOptie/name
		, 'WarmteAllocatie == Classifications/WarmteOptie/V/'+Classifications/GebiedsOptie/name+''
		, BO
		, bool), IsHidden = "True";

	container MeterVraag := BO/Metervraag, IsHidden = "True";
	
	container AardGas
	{
		attribute<nrAsl>  nrAansl_gas      (BO) := float64(BO/Aansluitingen/gnet > 0[nrAsl]) * BO/nrAansluitingen;
		attribute<Eur>    AansluitBijdrage (BO) := float64(BO/Aansluitingen/gnet > 0[nrAsl]) * BO/Opbrengsten/AansluitBijdrageG;
		attribute<Eur_yr> VastRecht        (BO) := float64(BO/Aansluitingen/gnet > 0[nrAsl]) * BO/Opbrengsten/VastRechtBijdrageG;

		unit<uint8> ggk := Prijzen/Aardgas/Staffel, IsHidden = "True"; // gebruiksgrootte klasse

		attribute<ggk>    ggk_rel          (BO) := classify(MeterVraag/Gas, ggk/ClassBreak);
		attribute<Eur_yr> eindgebruik_excl (BO) := MeterVraag/Gas * ggk/KGJ_eindgebruik_excl[ggk_rel];
		attribute<Eur_yr> Maatschappelijk  (BO) := MeterVraag/Gas * ggk/KGJ_Maatschappelijk [ggk_rel];
		attribute<Eur_yr> netwerk          (BO) := MeterVraag/Gas * ggk/KGJ_netwerk         [ggk_rel];
		attribute<Eur_yr> EnergieHeffing   (BO) := MeterVraag/Gas * ggk/KGJ_EnergieHeffing  [ggk_rel];
	}

	container Vastrecht
	{
		attribute<Eur_yr> Kj_Vastrecht_W   (BO) := float64(BO/Aansluitingen/Warmte > 0.0[nrAsl]) * BO/Opbrengsten/VastRechtMT_T;
		attribute<Eur_yr> Kj_Vastrecht_K   (BO) := float64(BO/Aansluitingen/Koude  > 0.0[nrAsl]) * BO/Opbrengsten/VastRechtK_T;

		attribute<Eur_yr> Kj_Vastrecht     (BO) := Kj_Vastrecht_W + Kj_Vastrecht_K;
	}

	attribute<nrAsl> aant_g_aansl_per_planregio       (PlanRegio) := sum((BO/Aansluitingen/Gnet)                            , BO/PlanRegio_rel), IsHidden = "True";
	attribute<nrAsl> aant_g_aansl_hoogb_per_planregio (PlanRegio) := sum((BO/Aansluitingen/Gnet) * float64( BO/IsMeergezins), BO/PlanRegio_rel), IsHidden = "True";
	attribute<nrAsl> aant_g_aansl_laagb_per_planregio (PlanRegio) := sum((BO/Aansluitingen/Gnet) * float64(!BO/IsMeergezins), BO/PlanRegio_rel), IsHidden = "True";

	container Elektriciteit
	{
		attribute<Eur>    AansluitBijdrage (BO) := BO/Opbrengsten/AansluitBijdrageE;
		attribute<Eur_yr> VastRecht        (BO) := BO/Opbrengsten/VastRechtBijdrageE;
		attribute<Eur_yr> Heffingskorting  (BO) := BO/Opbrengsten/HeffingskortingE;

		unit<uint8> ggk := Prijzen/Elektriciteit/Staffel, IsHidden = "True"; // gebruiksgrootte klasse

		attribute<ggk>    ggk_rel          (BO) := classify(MeterVraag/e, ggk/ClassBreak);
		attribute<Eur_yr> eindgebruik_excl (BO) := MeterVraag/e * ggk/KGJ_eindgebruik_excl[ggk_rel];
		attribute<Eur_yr> Maatschappelijk  (BO) := MeterVraag/e * ggk/KGJ_Maatschappelijk [ggk_rel];
		attribute<Eur_yr> netwerk          (BO) := MeterVraag/e * ggk/KGJ_netwerk         [ggk_rel];
		attribute<Eur_yr> EnergieHeffing   (BO) := MeterVraag/e * ggk/KGJ_EnergieHeffing  [ggk_rel] - Heffingskorting;
		attribute<Eur_yr> Om_Comfort       (BO) := BO/Functioneel/V_KD * ggk/KGJ_Maatschappelijk[ggk_rel];
	}
	container CO2
	{
		attribute<KG_yr> AardGas           (BO) := MeterVraag/gas      * prijzen/AardGas/CO2_GJ;
		attribute<KG_yr> Elektriciteit     (BO) := MeterVraag/e        * prijzen/Elektriciteit/CO2_GJ;
		attribute<KG_yr> Elek_e            (BO) := BO/Metervraag/e     * prijzen/Elektriciteit/CO2_GJ;
		attribute<KG_yr> Elek_app          (BO) := BO/Metervraag/EA    * prijzen/Elektriciteit/CO2_GJ;
		attribute<KG_yr> Pellets           (BO) := MeterVraag/Pellets  * prijzen/overig/CO2_pellets;
		attribute<KG_yr> Biomassa          (BO) := Metervraag/Biomassa * prijzen/overig/CO2_biomassa;
		attribute<KG_yr> Olie              (BO) := Metervraag/Olie     * prijzen/overig/CO2_olie;
	}
}

