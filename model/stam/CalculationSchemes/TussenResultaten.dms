//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) VESTA 2020 - Planbureau voor de Leefomgeving                   //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

container TussenResultaten
{
	#include <RekenstapT.dms>
	#include <BebouwingsComponentT.dms>
	#include <GebouwOptieT.dms>

	template DoelLabelT
	{
		// begin case parameters
		unit<uint32> BebouwingsObject;
		parameter<Classifications/DoelLabel_NaSprong> DoelLabel_rel;
		// end case parameters

		unit<uint8>  SchilSprong_set  := VerbruiksOpties/SchilSprong_set; // outward ref
		unit<uint32> SchilSprong_x_MO := VerbruiksOpties/SchilSprong_set/xMO;
		
		parameter<Units/yr> Threshold_bouwnorm := DefaultInstellingen/Basis/Beleid/Threshold_bouwnorm, Descr = "Jaar vanaf wanneer geen schilsprongen meer gemaakt worden bij onbekende labels";
		
		// alternatieve inschatting van-label voor bestaande woning met onbekend label: label met verbruik net onder dat van het object in kwestie
		attribute<Classifications/SchilLabel> Van_rel_alt(BebouwingsObject) :=	=BCname == 'BestaandeWoning' ? 
			'BebouwingsObject/SchilLabel_rel != Classifications/schillabel/V/N ? BebouwingsObject/SchillabelOrDefault_rel :
			BebouwingsObject/Interpolate/Defaultlabel_lower' : 'BebouwingsObject/SchillabelOrDefault_rel';
			
		attribute<Classifications/SchilLabel> Van_rel    (BebouwingsObject) := BebouwingsObject/SchillabelOrDefault_rel;
		parameter<Classifications/SchilLabel> Naar_rel                      := Classifications/DoelLabel_NaSprong/SchilLabel_rel[DoelLabel_rel];
		
		attribute<SchilSprong_set> SchilSprong_rel    (BebouwingsObject) := rlookup(combine_data(Classifications/SchilSprongCode, Van_rel, Naar_rel), VerbruiksOpties/SchilSprong_set/SchilSprongCode_rel);
			
		attribute<SchilSprong_x_MO> SchilSprong_xMO_rel(BebouwingsObject) := combine_data(VerbruiksOpties/schilsprong_set/xMO, SchilSprong_rel, BebouwingsObject/Model_rel);

		attribute<Eur> Ki_GV_def (BebouwingsObject) := MakeDefined
		(
			 SchilSprong_x_MO/Ki_asl[SchilSprong_xMO_rel] * BebouwingsObject/nrAansluitingen +
			 SchilSprong_x_MO/Ki_opp[SchilSprong_xMO_rel] * BebouwingsObject/Oppervlakte
			,0[Eur]
		), Descr = "Kosten van schilsprong. Bij onbekend schillabel: kosten vanaf schillabel met verbruik net boven dat van het object in kwestie";
		
		attribute<SchilSprong_set > SchilSprong_rel_alt     (BebouwingsObject) := rlookup(combine_data(Classifications/SchilSprongCode, Van_rel_alt, Naar_rel), VerbruiksOpties/SchilSprong_set/SchilSprongCode_rel);
			
		attribute<SchilSprong_x_MO> SchilSprong_xMO_rel_alt (BebouwingsObject) := combine_data(VerbruiksOpties/schilsprong_set/xMO, SchilSprong_rel_alt, BebouwingsObject/Model_rel);

		attribute<Eur> Ki_GV_alt (BebouwingsObject) := MakeDefined
		(
			 SchilSprong_x_MO/Ki_asl[SchilSprong_xMO_rel_alt] * BebouwingsObject/nrAansluitingen +
			 SchilSprong_x_MO/Ki_opp[SchilSprong_xMO_rel_alt] * BebouwingsObject/Oppervlakte
			,0[Eur]
		), Descr = "Alternatieve kosten van schilsprong voor gebouw met onbekend schillabel, gebaseerd om van-schillabel met verbruik net onder dat van het object in kwestie";
		
		attribute<Eur> Ki_GV (BebouwingsObject) := 	=BCname == 'BestaandeWoning' ? 
			'BebouwingsObject/SchilLabel_rel != Classifications/schillabel/V/N ? Ki_GV_def :
			(BebouwingsObject/Interpolate/R_lower * Ki_GV_alt + BebouwingsObject/Interpolate/R_upper * Ki_GV_def)'
			: 'Ki_GV_def'
			, Descr = "Investeringsbedrag van de schilsprong per bebouwingsobject";
	}
}