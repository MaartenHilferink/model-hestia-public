//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) Hestia 2021 - PBL & TNO                                        //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

template StateNaAllocatieComponent
{
	// begin case parameters
	unit<uint32> BO_base;
	// end case parameters

	unit<uint32> BO := BO_base
	{
		container Metervraag   := BO_base/Metervraag;
		container BC_kentallen := BO_base/BC_kentallen;

		container Aansluitingen := BO_base/Aansluitingen
		{
			attribute<nrAsl> Gnet  (BO) := nrAansluitingen * float64(not(Classifications/WarmteOptie/isGebiedsOptie[WarmteOptie_rel])) * float64(Metervraag/gas > 0[GJ_yr]);
			attribute<nrAsl> H2net (BO) := nrAansluitingen * float64(Classifications/WarmteOptie/IsH2[WarmteOptie_rel]);
			attribute<nrAsl> Warmte(BO) := nrAansluitingen * float64(Classifications/WarmteOptie/IsWarmteNet[WarmteOptie_rel] || WarmteOptie_rel == Classifications/WarmteOptie/V/Gebied_zK || WarmteOptie_rel == Classifications/WarmteOptie/V/Gebied_gK);
		}
		container WarmteVraag // CalculationSchemes/WarmteVraagT is hier expliciet geinstantieerd wegens iets andere toepassing bij herberekening achteraf.
		{
			// begin case parameters
			// attribute<Celsius> T_sec(BO) := Classifications/GebiedsOptie/T_sec[Classifications/WarmteOptie/GebiedsOptie_rel[WarmteOptie_rel]];
			attribute<bool>    IsLT(BO) := Classifications/WarmteOptie/IsLT[WarmteOptie_rel];
			attribute<bool>    Koudebeschikbaar(BO) := WarmteOptie_rel == Classifications/WarmteOptie/V/WKO; // TODO OV: Generaliseren
			attribute<float64> BeschikbaarheidsFactor(BO) := Float64(Classifications/WarmteOptie/IsGebiedsOptie[WarmteOptie_rel]);
			// end case parameters

			//Aansluitwaardes
			attribute<kW>     ASW_RV             (BO) := IsLT ? BO/AansluitCapaciteit/ASW_RVlt : BO/AansluitCapaciteit/ASW_RVmt;
			attribute<kW>     ASW_TW             (BO) := IsLT ? BO/AansluitCapaciteit/ASW_TWlt : BO/AansluitCapaciteit/ASW_TWmt;
			attribute<kW>     ASW_K              (BO) := KoudeBeschikbaar     ? BO/AansluitCapaciteit/ASW_K    : 0[kw];

			//Gelijktijdigheidsfactoren
			attribute<ratio>  GTF_RV             (BO) := IsLT ? BO/AansluitCapaciteit/GTF_RVlt : BO/AansluitCapaciteit/GTF_RVmt;
			attribute<ratio>  GTF_TW             (BO) := const(BO/AansluitCapaciteit/GTF_TW,BO);
			attribute<ratio>  GTF_K              (BO) := const(BO/AansluitCapaciteit/GTF_K,BO);

			//Vermogensvraag aan het secundaire net
			attribute<kW>     P_RV_sec           (BO) := ASW_RV * GTF_RV;
			attribute<kW>     P_TW_sec           (BO) := ASW_TW * GTF_TW;
			attribute<kW>     P_K_sec            (BO) := KoudeBeschikbaar ? ASW_K * GTF_K : 0.0[kw];

			attribute<kW>     AansluitCapaciteit (BO) := KoudeBeschikbaar ? max_elem(ASW_TW   + ASW_RV  , ASW_K  ) : ASW_TW   + ASW_RV  ;
			attribute<kW>     P_sec              (BO) := KoudeBeschikbaar ? max_elem(P_TW_sec + P_RV_sec, P_K_sec) : P_TW_sec + P_RV_sec;

			attribute<nrAsl>  nrAansluitingen    (BO) := BeschikbaarheidsFactor * BO/nrAansluitingen;
			attribute<m2>     Oppervlakte        (BO) := BeschikbaarheidsFactor * BO/Oppervlakte;

			attribute<GJ_yr>  V_RV               (BO) := BeschikbaarheidsFactor * BO/Functioneel/RV;
			attribute<GJ_yr>  V_TW               (BO) := BeschikbaarheidsFactor * BO/Functioneel/TW;
			attribute<GJ_yr>  V_KD               (BO) := BeschikbaarheidsFactor * BO/Functioneel/KD;
			attribute<GJ_yr>  V_Warmte           (BO) := V_RV + V_TW;
			attribute<Eur_yr> Oj_Warmte          (BO) := V_Warmte * BO/WarmteWet/WarmtePrijs, Source = "FO v7a F 8-10";
			attribute<Eur_yr> Oj_VastRechtMT     (BO) := BeschikbaarheidsFactor * BO/Opbrengsten/VastRechtMT_T;
			attribute<Eur_yr> Oj_VastRechtLT     (BO) := BeschikbaarheidsFactor * BO/Opbrengsten/VastRechtLT_T;
			attribute<Eur_yr> Oj_VastRechtK      (BO) := BeschikbaarheidsFactor * BO/Opbrengsten/VastRechtK_T;
			attribute<Eur>    Oi_AansluitBijdrage(BO) := BeschikbaarheidsFactor * BO/Opbrengsten/AansluitBijdrageT;
		}
		
		container eenmalig := for_each_nedv(
			Classifications/GebouwOptie_eenmalig/name,
			'BO_base/kosten/eenmalig/'+ Classifications/GebouwOptie_eenmalig/name,
			BO,
			Eur)
		{
			//Aansluitbijdrages
			attribute<Eur> Ki_Asl_Enet (BO) := BC_kentallen/Ki_Aansl_e_asl * nraansluitingen + BC_kentallen/Ki_Aansl_e_opp * oppervlakte;
			attribute<Eur> Ki_Asl_Gnet (BO) := Metervraag/gas <= 0[GJ_yr]
				? 0[Eur]
				: BC_kentallen/Ki_Aansl_g_asl * nraansluitingen + BC_kentallen/Ki_Aansl_g_opp * oppervlakte;
		}
		container Jaarlijks := for_each_nedv(
			Classifications/GebouwOptie_jaarlijks/name,
			'BO_base/kosten/jaarlijks/'+ Classifications/GebouwOptie_jaarlijks/name,
			BO,
			Eur_yr)
		{
			attribute<eur_yr> kji_gv (BO) :=BO_base/kosten/jaarlijks/Kji_gv;
			attribute<eur_yr> kmi_gv (BO) :=BO_base/kosten/jaarlijks/Kmi_gv;


			attribute<Invoer/Kengetallen/Eindgebruikerskosten/SSOKWT> HV_rel (BO) := rlookup((
				Classifications/Schillabel/name[BO/Energielabel_rel] + '_' + Classifications/Schillabel/name[BebouwingsComponenten/Woning/BO/Energielabel_rel] //Schilsprong
				+ '_' + Invoer/Kengetallen/Eindgebruikerskosten/OK/Label[classify(BO/oppervlakte, Invoer/Kengetallen/Eindgebruikerskosten/OK/ClassBreaks)]
				+ '_' + (BO/isMeergezins ? 'MGW' : 'EGW')
				),Invoer/Kengetallen/Eindgebruikerskosten/SSOKWT/Label)
				;
			attribute<eur_yr> Oj_hv  (BO) :=
				  BO/Eigendom_rel == Classifications/Eigendom/V/PartHuur ? Invoer/Kengetallen/Eindgebruikerskosten/SSOKWT/Oj_hv_Punten[HV_rel]
				: BO/Eigendom_rel == Classifications/Eigendom/V/WoonCorp ? Invoer/Kengetallen/Eindgebruikerskosten/SSOKWT/Oj_hv_WoonCo[HV_rel]
				: 0.0[eur_yr];
		}
		
		container Kapitaallasten
		{
			container PrevResults := BO_base/OrgKapitaallasten;

			//Aansluitbijdrages
			attribute<Eur_yr> Kji_Asl_Enet (BO) := PrevResults/Kji_Asl_Enet + eenmalig/Ki_Asl_Enet * NcwRefs/_50/AnnualisationFactor;
			attribute<Eur_yr> Kmi_Asl_Enet (BO) := PrevResults/Kmi_Asl_Enet + eenmalig/Ki_Asl_Enet * NCW/mr50/AnnualisationFactor;
			attribute<Eur_yr> Kji_Asl_Gnet (BO) := PrevResults/Kji_Asl_Gnet + eenmalig/Ki_Asl_Gnet * NcwRefs/_50/AnnualisationFactor;
			attribute<Eur_yr> Kmi_Asl_Gnet (BO) := PrevResults/Kmi_Asl_Gnet + eenmalig/Ki_Asl_Gnet * NCW/mr50/AnnualisationFactor;

			//Investeringen
			attribute<Eur_yr> Kji30_LO (BO) := PrevResults/Kji30_LO + eenmalig/Ki30_LO * NcwRefs/_30/AnnualisationFactor;
			attribute<Eur_yr> Kmi30_LO (BO) := PrevResults/Kmi30_LO + eenmalig/Ki30_LO * NCW/mr30/AnnualisationFactor;
			attribute<Eur_yr> Kji20_LO (BO) := PrevResults/Kji20_LO + eenmalig/Ki20_LO * NcwRefs/_20/AnnualisationFactor;
			attribute<Eur_yr> Kmi20_LO (BO) := PrevResults/Kmi20_LO + eenmalig/Ki20_LO * NCW/mr20/AnnualisationFactor;
			attribute<Eur_yr> Kji15_LO (BO) := eenmalig/Ki15_LO * NcwRefs/_15/AnnualisationFactor;
			attribute<Eur_yr> Kmi15_LO (BO) := eenmalig/Ki15_LO * NCW/mr15/AnnualisationFactor;
			attribute<Eur_yr> Kji_gv   (BO) := PrevResults/Kji_gv   + jaarlijks/Kji_gv;
			attribute<Eur_yr> Kmi_gv   (BO) := PrevResults/Kmi_gv   + jaarlijks/Kmi_gv;
			
			attribute<Eur_yr> Kji_LTAS (BO) := PrevResults/Kji_LTAS + eenmalig/Ki_LTAS * NcwRefs/_30/AnnualisationFactor;
			attribute<Eur_yr> Kmi_LTAS (BO) := PrevResults/Kmi_LTAS + eenmalig/Ki_LTAS * NCW/mr30/AnnualisationFactor;

			//Subsidies
			attribute<Eur_yr> Oji_s_LO_30 (BO) := PrevResults/Oji_s_LO_30 + eenmalig/Oi30_LO * NcwRefs/_30/AnnualisationFactor;
			attribute<Eur_yr> Oji_s_LO_20 (BO) := PrevResults/Oji_s_LO_20 + eenmalig/Oi20_LO * NcwRefs/_20/AnnualisationFactor;
			attribute<Eur_yr> Oji_s_LO_15 (BO) := PrevResults/Oji_s_LO_15 + eenmalig/Oi15_LO * NcwRefs/_15/AnnualisationFactor;
			attribute<Eur_yr> Oji_s_gv    (BO) := PrevResults/Oji_s_gv    + eenmalig/Oi_gv   * NcwRefs/_30/AnnualisationFactor;
			
		}
	}
}

