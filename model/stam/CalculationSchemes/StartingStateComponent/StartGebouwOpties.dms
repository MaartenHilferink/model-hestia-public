//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) Hestia 2023 - PBL & TNO                                        //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

container StartGebouwOpties
{
	//toekenning gebouwopties in startjaar

	//parameters
	container par
	{
		//parameters voor toekenning o.b.v. liggingsgegevens
		parameter<Classifications/SchilLabel>   label_cutoff_ewp := Classifications/SchilLabel/V/B          , descr = "energielabel vanaf welk niveau warmtepomp wordt toegekent bij onbekend verwarmingssysteem";
		parameter<Classifications/GebouwOptie>  optie_bioketel   := Classifications/GebouwOptie/V/Bioketel_zKD, descr = "GebouwOptie voor gebouwen op biomassa";
		parameter<Classifications/GebouwOptie>  optie_warmtepomp := Classifications/GebouwOptie/V/LweWP_zKD   , descr = "GebouwOptie voor gebouwen op warmtepomp";
		parameter<Classifications/GebouwOptie>  optie_warmtenet  := Classifications/GebouwOptie/V/Gebied_zK   , descr = "GebouwOptie voor gebouwen op warmtenet";
		parameter<Classifications/GebouwOptie>  optie_olieketel  := Classifications/GebouwOptie/V/Olie_zKD    , descr = "GebouwOptie voor gebouwen op olie";
		parameter<Classifications/GebouwOptie>  optie_HRketel    := Classifications/GebouwOptie/V/HR_zKD;
		
		//parameters voor kansberekening
		parameter<ratio> aandeelHR := Invoer/StartgegevensAan ? 0.75[ratio] : 0.00[ratio], descr = "aandeel gebouwen met moderne HR ketel i.t.t. verouderde VR ketel";

		//parameters oliekachels: aantallen per categorie woningen
		parameter<ratio> vrst_koop_1930               := (6166[float64] + 1191[float64]) / sum(float64(
			   BebouwingsType == Classifications/WoningType/V/vrijstaand
			&& Eigendom_rel == Classifications/Eigendom/V/koop
			&& Bouwjaar < 1930[yr_uint16]
			));
		parameter<ratio> vrst_koop_1931_1959          := (5054[float64] + 1117[float64]) / sum(float64(
			   BebouwingsType == Classifications/WoningType/V/vrijstaand
			&& Eigendom_rel == Classifications/Eigendom/V/koop
			&& (Bouwjaar >= 1930[yr_uint16] && Bouwjaar < 1960[yr_uint16])
			));
		parameter<ratio> vrst_koop_1960_1980          := (1941[float64] + 0137[float64]) / sum(float64(
			   BebouwingsType == Classifications/WoningType/V/vrijstaand
			&& Eigendom_rel == Classifications/Eigendom/V/koop
			&& (Bouwjaar >= 1960[yr_uint16] && Bouwjaar < 1980[yr_uint16])
			));

		parameter<ratio> vrst_parthuur_1930           := (0737[float64] + 0234[float64]) / sum(float64(
			   BebouwingsType == Classifications/WoningType/V/vrijstaand
			&& Eigendom_rel == Classifications/Eigendom/V/parthuur
			&& Bouwjaar < 1930[yr_uint16]
			));
		parameter<ratio> vrst_parthuur_1931_1959      := (0469[float64] + 0020[float64]) / sum(float64(
			   BebouwingsType == Classifications/WoningType/V/vrijstaand
			&& Eigendom_rel == Classifications/Eigendom/V/parthuur
			&& (Bouwjaar >= 1930[yr_uint16] && Bouwjaar < 1960[yr_uint16])
			));
		parameter<ratio> vrst_parthuur_1960_1980      := (0112[float64] + 0024[float64]) / sum(float64(
			   BebouwingsType == Classifications/WoningType/V/vrijstaand
			&& Eigendom_rel == Classifications/Eigendom/V/parthuur
			&& (Bouwjaar >= 1960[yr_uint16] && Bouwjaar < 1980[yr_uint16])
			));

		parameter<ratio> hoek_21kp_koop_1930          := (2437[float64] + 0457[float64]) / sum(float64(
			   (BebouwingsType == Classifications/WoningType/V/twee_onder_1_kap || BebouwingsType == Classifications/WoningType/V/hoekwoning)
			&& Eigendom_rel == Classifications/Eigendom/V/koop
			&& Bouwjaar < 1930[yr_uint16]
			));
		parameter<ratio> hoek_21kp_koop_1931_1959     := (1421[float64] + 0492[float64]) / sum(float64(
			   (BebouwingsType == Classifications/WoningType/V/twee_onder_1_kap || BebouwingsType == Classifications/WoningType/V/hoekwoning)
			&& Eigendom_rel == Classifications/Eigendom/V/koop
			&& (Bouwjaar >= 1930[yr_uint16] && Bouwjaar < 1960[yr_uint16])
			));
		parameter<ratio> hoek_21kp_koop_1960_1980     := (3635[float64] + 0000[float64]) / sum(float64(
			   (BebouwingsType == Classifications/WoningType/V/twee_onder_1_kap || BebouwingsType == Classifications/WoningType/V/hoekwoning)
			&& Eigendom_rel == Classifications/Eigendom/V/koop
			&& (Bouwjaar >= 1960[yr_uint16] && Bouwjaar < 1980[yr_uint16])
			));

		parameter<ratio> hoek_21kp_parthuur_1930      := (0250[float64] + 0267[float64]) / sum(float64(
			   (BebouwingsType == Classifications/WoningType/V/twee_onder_1_kap || BebouwingsType == Classifications/WoningType/V/hoekwoning)
			&& Eigendom_rel == Classifications/Eigendom/V/parthuur
			&& Bouwjaar < 1930[yr_uint16]
			));
		parameter<ratio> hoek_21kp_parthuur_1931_1959 := (0196[float64] + 0000[float64]) / sum(float64(
			   (BebouwingsType == Classifications/WoningType/V/twee_onder_1_kap || BebouwingsType == Classifications/WoningType/V/hoekwoning)
			&& Eigendom_rel == Classifications/Eigendom/V/parthuur
			&& (Bouwjaar >= 1930[yr_uint16] && Bouwjaar < 1960[yr_uint16])
			));
		parameter<ratio> hoek_21kp_parthuur_1960_1980 := (0289[float64] + 0000[float64]) / sum(float64(
			   (BebouwingsType == Classifications/WoningType/V/twee_onder_1_kap || BebouwingsType == Classifications/WoningType/V/hoekwoning)
			&& Eigendom_rel == Classifications/Eigendom/V/parthuur
			&& (Bouwjaar >= 1960[yr_uint16] && Bouwjaar < 1980[yr_uint16])
			));

		parameter<uint32> HRseed := /Invoer/TechnischeInstellingen/RandomSeed * 314[uint32];
		parameter<uint32> Oseed  := /Invoer/TechnischeInstellingen/RandomSeed * 315[uint32];
	}

	//heeft gebouw een warmtenetaansluiting? initiele check o.b.v. ligging warmtenetten (gebouw ligt in warmtenetgebied, en dat gebied is eerder dan eerste rekenjaar aangelegd)
	attribute<bool> AllocWarmtenet  (results) := isdefined(BestaandAflevergebied_rel) && BestaandAflevergebied/StartJaar_or_2000[BestaandAflevergebied_rel] <= first(/Rekenstap/Jaar);

	//heeft gebouw een warmtepomp? initiele check o.b.v. ligging gasnetten en warmtenetten (gebouw ligt niet in warmtenet, is wel gasloos, en heeft een goed label)
	attribute<bool> AllocWarmtepomp (results) := gasloos && not(AllocWarmtenet) && Defaultlabel_rel < par/label_cutoff_ewp;

	//heeft gebouw een bioketel? initiele check o.b.v. ligging gasnetten en warmtenetten (overige gasloze gebouwen gebouwen)
	attribute<bool> AllocBioketel   (results) := gasloos && not(AllocWarmtenet) && not(AllocWarmtepomp);

	//heeft gebouw een oliekachel? o.b.v. sampling
	attribute<float64> rnd_1        (results) := rnd_uniform(par/HRseed, results, range(float64, 0d, 1d));
	attribute<bool> AllocOlieketel  (results) := not(AllocWarmtenet) && not(AllocWarmtepomp) && not(AllocBioketel) && (
			  BebouwingsType == Classifications/WoningType/V/vrijstaand ?
				( Eigendom_rel == Classifications/Eigendom/V/koop ?
				  	( Bouwjaar < 1930[yr_uint16] ? (rnd_1 < par/vrst_koop_1930)
				  	: Bouwjaar < 1960[yr_uint16] ? (rnd_1 < par/vrst_koop_1931_1959)
				  	: Bouwjaar < 1980[yr_uint16] ? (rnd_1 < par/vrst_koop_1960_1980)
				  	: false)
				: Eigendom_rel == Classifications/Eigendom/V/parthuur ?
					( Bouwjaar < 1930[yr_uint16] ? (rnd_1 < par/vrst_parthuur_1930)
				  	: Bouwjaar < 1960[yr_uint16] ? (rnd_1 < par/vrst_parthuur_1931_1959)
				  	: Bouwjaar < 1980[yr_uint16] ? (rnd_1 < par/vrst_parthuur_1960_1980)			
				  	: false) : false)
			: BebouwingsType == Classifications/WoningType/V/twee_onder_1_kap || BebouwingsType == Classifications/WoningType/V/hoekwoning ?
				( Eigendom_rel == Classifications/Eigendom/V/koop ?
				  	( Bouwjaar < 1930[yr_uint16] ? (rnd_1 < par/hoek_21kp_koop_1930)
				  	: Bouwjaar < 1960[yr_uint16] ? (rnd_1 < par/hoek_21kp_koop_1931_1959)
				  	: Bouwjaar < 1980[yr_uint16] ? (rnd_1 < par/hoek_21kp_koop_1960_1980)
				  	: false)
				: Eigendom_rel == Classifications/Eigendom/V/parthuur ?
					( Bouwjaar < 1930[yr_uint16] ? (rnd_1 < par/hoek_21kp_parthuur_1930)
				  	: Bouwjaar < 1960[yr_uint16] ? (rnd_1 < par/hoek_21kp_parthuur_1931_1959)
				  	: Bouwjaar < 1980[yr_uint16] ? (rnd_1 < par/hoek_21kp_parthuur_1960_1980)
				  	: false) : false)
			: false
		);

	//heeft gebouw een moderne HR ketel? o.b.v. sampling
	attribute<float64> rnd_2        (results) := rnd_uniform(par/HRseed, results, range(float64, 0d, 1d));
	attribute<bool> AllocHRketel    (results) := not(AllocWarmtenet) && not(AllocWarmtepomp) && not(AllocBioketel) && not(AllocOlieketel) && float64(par/aandeelHR) > rnd_2;



	attribute<Classifications/GebouwOptie> GebouwOptie_rel (results) :=
				AllocBioketel   ? par/optie_bioketel
			:	AllocWarmtepomp ? par/optie_warmtepomp
			:	AllocWarmtenet  ? par/optie_warmtenet
			:	AllocOlieketel  ? par/optie_olieketel
			:   AllocHRketel    ? par/optie_HRketel
			:	ModelObject/StartOptie_rel[ModelObject_rel];
}
