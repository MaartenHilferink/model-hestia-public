//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) Hestia 2021 - Planbureau voor de Leefomgeving                  //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//           Dit is de container met rekenschema's die werken op Bebouwing              //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

//============== AcieveWoning pakketbepaling =====
unit<uint32> BouwdeelActieveWoning := subset(not(BO/Afbouw) && BO/Activatie/BouwdeelActief)
{
	attribute<BO> BO_rel := nr_OrgEntity;
	attribute<string> code := BO/code[BO_rel];
	attribute<dpoint> Geometry := BO/Geometry[BO_rel];
	
	container IsolatieNiveau_rel := for_each_nedv(classifications/bouwdeel/name, 'BO/Bouwdelen/Isolatie/'+classifications/bouwdeel/name+'[BO_rel]', ., Classifications/IsolatieNiveau);
		
	container IsolatieAmbities := for_each_ne(replace_value(classifications/IsolatieAmbitie/name, 'geen', '')
	,	replace('AmbitieT(.., classifications/IsolatieAmbitie/BesparingsFactor[@A])'
		, '@A', 'classifications/IsolatieAmbitie/V/'+classifications/IsolatieAmbitie/name)
	)
	{
		container geen // overerf bij 'geen' bouwdeel isolatieniveaus's die er al zijn
		{
			container KiesBouwdelen := for_each_nedv(
				classifications/Bouwdeel/name+'/IsolatieNiveau_rel', 
				'BouwdeelActieveWoning/IsolatieNiveau_rel/'+classifications/Bouwdeel/name,
				BouwdeelActieveWoning,
				Classifications/IsolatieNiveau);
		}
	}
	
	
	parameter<uint32> RandomSeed := 0;
	
	template AmbitieT 
	{
		unit<uint32> ActieveWoning;
		parameter<float64> beta;
		
		parameter<GJ_Yr>   VerbruiksIndicatie := 1000[m3_yr] * 32.68[GJ_m3]; // indicatie verbruik per woning in GJ_Jr
		parameter<EUR_GJ>  EnergiePrijs := 0.6[EUR_m3] / 32.68[GJ_m3]; // indicatie kosten per GJ
		parameter<Yr>      Duration := 20[yr]; // afschrijftijd voor rentabiliteitsafweging
		
		attribute<m2> TotalArea(ActieveWoning) := ='add('+replace('BO/Bouwdelen/@BD[nr_OrgEntity]','@BD', classifications/IsolatieMaatregel/bouwdeel_name)+')';
		
		parameter<EUR> CombiFactor := beta * VerbruiksIndicatie * EnergiePrijs * Duration;
		
		attribute<Float64> BesparingsFactor(classifications/BouwdeelIsolatie) := // hoeveel besparing in verlies; TODO: varieren KG, MG, GG 		
			classifications/Bouwdeel/Verlies[classifications/BouwdeelIsolatie/bouwdeel_rel] // hoeveel relatief belang 
		*	MakeDefined(classifications/IsolatieMaatregel/R_RV/MG[invert(classifications/IsolatieMaatregel/BouwdeelIsolatie_rel)] / value(100, Percent), 0.0);
			
		attribute<EUR> Besparing(classifications/BouwdeelIsolatie) := BesparingsFactor * Combifactor; // TODO: varieren KG, MG, GG en aansluiten op statisch berekende factoren
		
		attribute<EUR_m2> Kosten   (classifications/BouwdeelIsolatie) :=  // tov kosten [per m2] TODO: varieren naar in context van toepassing zijnde kostenkolom. Allereerst Egw, Mgw
			MakeDefined(classifications/IsolatieMaatregel/kosten/Nat_Egw_Ind[invert(classifications/IsolatieMaatregel/BouwdeelIsolatie_rel)], 0[EUR_m2]);
		
		container IsolatieScore := for_each_nedv(classifications/BouwdeelIsolatie/name
		,	replace(
				'iif(IsolatieNiveau_rel/@BD <=  @I'
				',	BO/Bouwdelen/@BD[nr_OrgEntity]'
					'*	(	Besparing[@I]  / TotalArea'
						'-	iif(IsolatieNiveau_rel/@BD > @I, kosten[@I], 0[EUR_m2])'
						')'
				',	-10.0'
				')'
			,	'@BD', classifications/BouwdeelIsolatie/bouwdeel_name
			,	'@I',  'classifications/BouwdeelIsolatie/V/'+classifications/BouwdeelIsolatie/name
			)
		, 	ActieveWoning, float64
		);
		
		// TODO: check for overflow
		parameter<float64> LOG_MAX_FLOAT64 := 700.0; // slightly less than ln( 2^1024 / pcount(classifications/BouwdeelIsolatie/bouwdeel_rel) )
		container OddsRatio := for_each_nedv(classifications/BouwdeelIsolatie/name
		, 'exp(min_elem(IsolatieScore/'+classifications/BouwdeelIsolatie/name+' * beta, LOG_MAX_FLOAT64))'
		, BouwdeelActieveWoning, float64
		)
		{
			container Total := for_each_nedv(classifications/bouwdeel/name
			, 'add('+AsItemList(classifications/BouwdeelIsolatie/name, classifications/BouwdeelIsolatie/bouwdeel_rel)+')'
			, BouwdeelActieveWoning, float64
			);
		}
		
		container P := for_each_nedv(classifications/BouwdeelIsolatie/name
		, 'OddsRatio/'+classifications/BouwdeelIsolatie/name + ' / OddsRatio/Total/'+classifications/BouwdeelIsolatie/bouwdeel_name
		, BouwdeelActieveWoning, float64
		);
			
		container KiesBouwdelen := for_each_ne(classifications/Bouwdeel/name
		,	'KiesBouwDeelT(classifications/Bouwdeel/V/' + classifications/Bouwdeel/name +')'
		);
	}
	template KiesBouwdeelT
	{
		parameter<classifications/Bouwdeel> bouwdeel_rel;
		
		container CumulP := for_each_nedv(classifications/IsolatieNiveau/name
		, 'P/'+classifications/bouwdeel/name[bouwdeel_rel]+'_'+classifications/IsolatieNiveau/name
		+ MakeDefined('+ '+classifications/IsolatieNiveau/name[ID(classifications/IsolatieNiveau)-1], '')
		, BouwdeelActieveWoning, float64
		);
		
		parameter<uint32> Seed := 0; // TODO: Koppelen aan generieke reproduceerbare seeder
		attribute<float64> rnd (BouwdeelActieveWoning) := rnd_uniform(Seed + bouwdeel_rel, BouwdeelActieveWoning, range(float64, 0.0, 1.0));
		attribute<classifications/IsolatieNiveau> IsolatieNiveau_rel(BouwdeelActieveWoning) := ='argmax('+AsItemList('CumulP/'+classifications/IsolatieNiveau/name+' >= rnd')+')[classifications/IsolatieNiveau]';
	}
	
	//Placeholder voorbeeld hoe resultscontainer er uit moet zien, voor gebruik in ProductActieveWoning (waarden tijdelijk constant gegeven)
	container spook_results 
	{
		unit<uint32> IAxAW := combine(classifications/IsolatieAmbitie, BouwdeelActieveWoning)
		{
			container Isolatie := for_each_nedv(
				classifications/Bouwdeel/name,
				replace(
					'union_data(IAxAW, '+AsItemList(replace('IsolatieAmbities/@IA/KiesBouwdelen/@BD/IsolatieNiveau_rel', '@IA', Classifications/IsolatieAmbitie/name))+')'
					,'@BD', classifications/Bouwdeel/name),
				IAxAW,
				Classifications/IsolatieNiveau);
		}
		unit<uint32> AWxIA := combine(BouwdeelActieveWoning, classifications/IsolatieAmbitie)
		,	Descr = "iedere (relevante) isolatie ambitie voor iedere op bouwdeel geactiveerde woning"  //AKA Spookwoning
		{
			attribute<BouwdeelActieveWoning>           BouwdeelActieveWoning_rel := nr_1;
			attribute<classifications/IsolatieAmbitie> IsolatieAmbitie_rel := nr_2;
			
			attribute<BO> BO_rel := BouwdeelActieveWoning/BO_rel[BouwdeelActieveWoning_rel];
			
			attribute<IAxAW> IAxAW_rel := combine_data(IAxAW, nr_2, nr_1);
			
			container Bouwdelen
			{
				container Isolatie := for_each_nedv(
					classifications/Bouwdeel/name,
					replace(
						'IAxAW/Isolatie/@BD[IAxAW_rel]'
						,'@BD', classifications/Bouwdeel/name),
					AWxIA,
					Classifications/IsolatieNiveau)
				{
					attribute<Classifications/Schillabel> Schillabel_rel (...) := const(Classifications/Schillabel/V/B,...); //TODO: methode schillabel afleiden van isolatiegraad
				}
				container IsChangedNow := for_each_nedv(
					classifications/Bouwdeel/name,
					replace('Isolatie/@BD > BO/Bouwdelen/isolatie/@BD[BO_rel]'
					,	'@BD', classifications/Bouwdeel/name),
					AWxIA,
					bool);
					
				container IsolatieMaatregel_rel := for_each_nedv(
					classifications/Bouwdeel/name,
					replace(
						'not(IsChangedNow/@BD) '
							'? (0/0)[Classifications/IsolatieMaatregel] '
							': classifications/BouwdeelIsolatie/IsolatieMaatregel_rel[combine_data(classifications/BouwdeelIsolatie, classifications/Bouwdeel/V/@BD , Isolatie/@BD)]'
					,	'@BD', classifications/Bouwdeel/name),
					AWxIA, 
					Classifications/IsolatieMaatregel);
				
				container Ki_gv := for_each_nedv(
					classifications/Bouwdeel/name,
					replace('makedefined(BO/Bouwdelen/@BD[BO_rel] * Classifications/IsolatieMaatregel/kosten/Zst_Egw_Ind[IsolatieMaatregel_rel/@BD], 0.0[Eur])' //TODO: variatie kostenkental
					,	'@BD', classifications/Bouwdeel/name),
					AWxIA, 
					Eur)
				{
					attribute<Eur> Totaal (...):= ='add('+AsItemList(Classifications/bouwdeel/name)+')';
				}
			}
			container Functioneel
			{
				attribute<GJ_yr> VT (..) := BO/Functioneel/VT[BO_rel];
				attribute<GJ_yr> KK (..) := BO/Functioneel/KK[BO_rel];
				attribute<GJ_yr> EA (..) := BO/Functioneel/EA[BO_rel];
				attribute<GJ_yr> TW (..) := BO/Functioneel/TW[BO_rel];
				attribute<GJ_yr> KD (..) := BO/Functioneel/KD[BO_rel];
				attribute<GJ_yr> RV (..) := RV_per_Bouwdeel_now/Totaal * Correction_RV;
				
				attribute<Kwh_yr_m2> Kwh_m2 (..) := (RV_per_Bouwdeel_now/Totaal / 3.6[MJ / kWh] * 1000 [ MJ / GJ]) / BO/Oppervlakte[BO_rel];
				
				attribute<ratio> Correction_RV (..) := 1.0[ratio] + Invoer/Kengetallen/Constanten/Rebound_A * Invoer/Kengetallen/Constanten/Rebound_B ^ Kwh_m2;
			
				container BouwdeelIsolatie_rel := for_each_nedv(
					classifications/Bouwdeel/name,
					'not(Bouwdelen/IsChangedNow/'+classifications/Bouwdeel/name+
					')? rlookup('+quote(classifications/Bouwdeel/name+ '_')+' + Classifications/IsolatieNiveau/name[BO/Bouwdelen/Isolatie/' + classifications/Bouwdeel/name + '[BO_rel][Classifications/IsolatieNiveau]], Classifications/BouwdeelIsolatie/name)
					: rlookup('+quote(classifications/Bouwdeel/name+ '_')+' + Classifications/IsolatieNiveau/name[Bouwdelen/Isolatie/' + classifications/Bouwdeel/name + '[Classifications/IsolatieNiveau]], Classifications/BouwdeelIsolatie/name)',
					..,
					Classifications/BouwdeelIsolatie);
				
				container RV_per_Bouwdeel_now := for_each_nedv(
					classifications/Bouwdeel/name,
					'BO/Functioneel/RV_N1_per_Bouwdeel_now/'+classifications/Bouwdeel/name+'[BO_rel] * (1.0[ratio] - makedefined(Classifications/IsolatieMaatregel/R_RV/MG[Classifications/BouwdeelIsolatie/IsolatieMaatregel_rel[BouwdeelIsolatie_rel/'+classifications/Bouwdeel/name+']] / 100[percent], 1.0[ratio]))',
					..,
					GJ_yr) //TODO: R_RV afhankelijk van profiel
				{
					attribute<GJ_yr> Totaal (...):= ='add('+AsItemList(Classifications/bouwdeel/name)+')';
				}
			}
			
			
			
			container Aansluitcapaciteit;
			// hier te berekenen: Funcionele vraag en Aansluitcapaciteit
		}
		
		attribute<bool> Inactief(BO) := not(not(BO/Afbouw) && BO/Activatie/BouwdeelActief);
		unit<uint32> Inactief_selected := select_unit(Inactief);
		
		unit<uint32> AWxIAuIW := union_unit(AWxIA, Inactief_selected)
		,	Descr = "union van AWxIA met niet overlappende niet bouwdeel geactiveerde woningen"
		{
			attribute<BO> BO_rel := union_data(AWxIAuIW, AWxIA/BO_rel, select_data(Inactief_selected, ID(BO)));
			
			attribute<Classifications/IsolatieAmbitie> IsolatieAmbitie_rel := union_data(AWxIAuIW, AWxIA/IsolatieAmbitie_rel, const((0/0)[Classifications/IsolatieAmbitie], Inactief_selected) );
			
			container Bouwdelen
			{
				container Isolatie := for_each_nedv(classifications/Bouwdeel/name
				,	replace(
						'union_data(AWxIAuIW, AWxIA/Bouwdelen/Isolatie/@BD, select_data(Inactief_selected, BO/Bouwdelen/Isolatie/@BD))'
					,	'@BD', classifications/Bouwdeel/name
					)
				,	AWxIAuIW,  Classifications/IsolatieNiveau
				);
				
				container Ki_gv := for_each_nedv(
					classifications/Bouwdeel/name,
					'union_data(AWxIAuIW, AWxIA/Bouwdelen/Ki_gv/'+classifications/Bouwdeel/name+', const(0.0[Eur], Inactief_selected))',	
					AWxIAuIW,
					Eur)
				{
					attribute<Eur> Totaal (...):= ='add('+AsItemList(Classifications/bouwdeel/name)+')';
				}
				
			}
			
		}
	}
	
	container results := for_each_ne(classifications/IsolatieAmbitie/name, 'ResultsT(classifications/IsolatieAmbitie/V/'+classifications/IsolatieAmbitie/name+')');
	template resultsT
	{
		parameter<classifications/IsolatieAmbitie> IsolatieAmbitie_rel;
		
		attribute<spook_results/AWxIA> AWxIA_rel(BouwdeelActieveWoning) := combine_data(spook_results/AWxIA, ID(BouwdeelActieveWoning), IsolatieAmbitie_rel);
		
		//uitkomsten per bouwdeel per ambitieniveau
		container bouwdelen := for_each_nedv(classifications/Bouwdeel/name,
			replace('spook_results/AWxIA/Bouwdelen/Isolatie/@BD[AWxIA_rel]'
			,	'@BD', classifications/Bouwdeel/name)
		,	BouwdeelActieveWoning, classifications/IsolatieNiveau
		);
			
		
		//kosten van isolatiemaatregelen indien dit ambitieniveau wordt toegepast
		container kosten := for_each_nedv(classifications/Bouwdeel/name,
			replace('spook_results/AWxIA/Bouwdelen/Ki_gv/@BD[AWxIA_rel]',	'@BD', classifications/Bouwdeel/name)
		,	BouwdeelActieveWoning, eur)
		{
			attribute<eur> Ki_gv(BouwdeelActieveWoning) := ='add('+AsItemList(classifications/Bouwdeel/name)+')';
		}
		
		//nieuwe functionele vraag indien dit ambitieniveau wordt toegepast
		container functioneel
		{
			attribute<GJ_yr> RV (BouwdeelActieveWoning) := spook_results/AWxIA/functioneel/RV[AWxIA_rel];
			attribute<GJ_yr> KD (BouwdeelActieveWoning) := const( 7.1[GJ_yr], BouwdeelActieveWoning);
			attribute<GJ_yr> VT (BouwdeelActieveWoning) := const( 1.5[GJ_yr], BouwdeelActieveWoning);
			
			attribute<GJ_yr> TW (BouwdeelActieveWoning) := BO/Functioneel/TW[nr_orgentity];
			attribute<GJ_yr> EA (BouwdeelActieveWoning) := BO/Functioneel/EA[nr_orgentity];
			attribute<GJ_yr> KK (BouwdeelActieveWoning) := BO/Functioneel/KK[nr_orgentity];
			
			attribute<Classifications/Schillabel> Schillabel_rel (BouwdeelActieveWoning) := BO/Schillabel_rel[nr_orgentity]; //indeling in schillabels o.b.v. functionele vraag -> herbereken met nieuwe Vrv
		}
		container AansluitCapaciteit
		{
			attribute<kW> RV (BouwdeelActieveWoning) := functioneel/RV * 0.25[kW_GJ_yr]; //later invullen (nu dummygetal)
			attribute<kW> KD (BouwdeelActieveWoning) := BO/AansluitCapaciteit/KD[nr_orgentity];
			attribute<kW> TW (BouwdeelActieveWoning) := BO/AansluitCapaciteit/TW[nr_orgentity];
		}
	}
	
	//TODO: voor woningen die alleen actief zijn op bouwdelen en niet op producten: hier afweging maken definitieve allocatie
	attribute<bool> ProductActief := BO/Activatie/ProductActief[nr_orgentity];
	unit<uint32> ProductInactieveBO := subset(not(ProductActief))
	{
		attribute<string> code := BouwdeelActieveWoning/code[nr_orgentity];
		container Afweging
		{
			attribute<Classifications/IsolatieAmbitie> Ambitie_chosen (..) := const(Classifications/IsolatieAmbitie/V/geen, ProductInactieveBO); //Verwijzen naar gekozen niveau (nu constant als placeholder)
			attribute<Classifications/Schillabel>      Schillabel_rel (..) := ='merge(Ambitie_chosen, Classifications/Schillabel, '+AsItemList('Results/'+Classifications/IsolatieAmbitie/name+'/functioneel/Schillabel_rel[nr_orgentity]')+')';
			
			container bouwdelen //uitkomsten per bouwdeel per ambitieniveau
			{
				container isolatie := for_each_nedv(
					Classifications/Bouwdeel/name,
					'merge(Ambitie_chosen,Classifications/IsolatieNiveau, '+
						replace(AsItemList( replace('Results/@IA@/Bouwdelen/@BD@[nr_orgentity]', '@IA@', Classifications/IsolatieAmbitie/name)), '@BD@', Classifications/Bouwdeel/name )+')',
					...,
					Classifications/IsolatieNiveau);
			}
			container kosten := for_each_nedv(
					Classifications/Bouwdeel/name,
					'merge(Ambitie_chosen,Eur, '+
						replace(AsItemList( replace('Results/@IA@/kosten/@BD@[nr_orgentity]', '@IA@', Classifications/IsolatieAmbitie/name)), '@BD@', Classifications/Bouwdeel/name )+')',
					..,
					Eur)
			{
				attribute<eur> Ki_gv (...) := ='merge(Ambitie_chosen, eur, '+AsItemList('Results/'+Classifications/IsolatieAmbitie/name+'/kosten/Ki_gv')+')';
			}
			container functioneel := for_each_nedv(
					Classifications/FunctioneleVraag/name,
					'merge(Ambitie_chosen,GJ_yr, '+
						replace(AsItemList( replace('Results/@IA@/functioneel/@PT@[nr_orgentity]', '@IA@', Classifications/IsolatieAmbitie/name)), '@PT@', Classifications/FunctioneleVraag/name )+')',
					..,
					GJ_yr);
			container AansluitCapaciteit := for_each_nedv(
					Classifications/ProductType/name,
					'merge(Ambitie_chosen,kW, '+
						replace(AsItemList( replace('Results/@IA@/AansluitCapaciteit/@PT@[nr_orgentity]', '@IA@', Classifications/IsolatieAmbitie/name)), '@PT@', Classifications/ProductType/name )+')',
					..,
					kW);
		}
		container ResultsPerBO
		{
			attribute<uint32>                     ProductInactieveBO_rel (BO) := rlookup(BO/code, ProductInactieveBO/code);
			attribute<Classifications/Schillabel> Schillabel_rel         (BO) := Afweging/Schillabel_rel[ProductInactieveBO_rel];
			
			container bouwdelen
			{
				container isolatie:= for_each_nedv(
					Classifications/Bouwdeel/name,
					'Afweging/bouwdelen/isolatie/'+Classifications/Bouwdeel/name+'[ProductInactieveBO_rel]',
					BO,
					Classifications/IsolatieNiveau);
			}
			container kosten := for_each_nedv(
					Classifications/Bouwdeel/name,
					'Afweging/kosten/'+Classifications/Bouwdeel/name+'[ProductInactieveBO_rel]',
					BO,
					Eur)
			{
				attribute<eur> Ki_gv (BO) := Afweging/kosten/Ki_gv[ProductInactieveBO_rel];
			}
			container functioneel := for_each_nedv(
				Classifications/FunctioneleVraag/name,
				'Afweging/functioneel/'+Classifications/FunctioneleVraag/name+'[ProductInactieveBO_rel]',
				BO,
				GJ_yr);
			container aansluitcapaciteit := for_each_nedv(
				Classifications/ProductType/name,
				'Afweging/aansluitcapaciteit/'+Classifications/ProductType/name+'[ProductInactieveBO_rel]',
				BO,
				kW);
			
			container metervraag := for_each_nedv(
				Classifications/BemeterdeGebouwInput/name,
				'const(10[GJ_yr],BO)',
				BO,
				GJ_yr);//TODO: recalculate metervraag met huidige installaties
		}
	}
}