//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) Hestia 2021 - PBL & TNO                                        //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

template WaterstofData
{
	// begin case parameters
	unit<uint32>    BO;
	parameter<bool> IsNieuwbouw;
	parameter<bool> IsWoning;
	parameter<Classifications/GebiedsOptie> Optie;
	// end case parameters

	unit<uint32>         PlanRegio         := Invoer/SpecifiekeInstellingen/PlanRegio;
	attribute<PlanRegio> Planregio_rel(BO) := BO/Planregio_rel;
	parameter<bool>      IsHybride         := Optie == Classifications/GebiedsOptie/V/WaterstofWP;

	//DEELNAME
	attribute<bool>  KanGebiedsOptie    (BO) := Classifications/WarmteOPtie/kanGebiedsOptie[ BO/WarmteOptie_rel ];
	attribute<bool>  LabelOK            (BO) := IsHybride ? BO/SchillabelOrDefault_rel <= /Classifications/Schillabel/V/E : const(true, BO);
	attribute<bool>  WPgeschikt         (BO) := KanGebiedsOptie && LabelOK;
	attribute<nrAsl> nrAansluitingen    (BO) := BO/nrAansluitingen;
	attribute<kw>    AansluitCapaciteit (BO) := BO/AansluitCapaciteit/Warmte;
	attribute<m2>    oppervlakte        (BO) := BO/oppervlakte;

	//VRAAG
	attribute<GJ_yr> V_RV               (BO) := float64(KanGebiedsOptie) * BO/BebouwingsObjectMetGebouwOptie/Functioneel/RV , Descr = "vraag naar ruimteverwarming";
	attribute<GJ_yr> V_TW               (BO) := float64(KanGebiedsOptie) * BO/BebouwingsObjectMetGebouwOptie/Functioneel/TW , Descr = "vraag naar warm tapwater";

	parameter<Classifications/GebouwOptie> GebouwOptie_rel := =IsWoning ?
		'Classifications/GebiedsOptie/GebouwOptie_rel[Optie]' :
		'Classifications/GebiedsOptie/GebouwOptie_rel_u[Optie]';
		
	container VerbruiksOpties := BO/BebouwingsComponent_UpRef/VerbruiksOpties; // to be used in GebouwOptieT

	container Installatie := CalculationSchemes/TussenResultaten/GebouwOptieT(BO, GebouwOptie_rel, 'Criteria/Always');
	
	attribute<GJ_yr>  V_gas       (BO) := float64(KanGebiedsOptie) * Installatie/Metervraag_aggr/Gas;
	attribute<GJ_yr>  V_H2        (BO) := float64(KanGebiedsOptie) * Installatie/Metervraag_aggr/H2;
	attribute<GJ_yr>  V_e         (BO) := float64(KanGebiedsOptie) * Installatie/Metervraag_aggr/e + Installatie/eEffect/totaal;

	attribute<Eur_yr> Kj_elek_EH  (BO) := float64(KanGebiedsOptie) * V_e * Prijzen_elec/KGJ_EnergieHeffing  [classify(Installatie/metervraag/e + BO/Functioneel/EA * Invoer/Kengetallen/Constanten/Efficiency_EA, Prijzen_elec/ClassBreak )];
	attribute<Eur_yr> Kj_elek_CO2 (BO) := float64(KanGebiedsOptie) * V_e * Prijzen_elec/KGJ_CO2Heffing      [classify(Installatie/metervraag/e + BO/Functioneel/EA * Invoer/Kengetallen/Constanten/Efficiency_EA, Prijzen_elec/ClassBreak )];
	attribute<Eur_yr> Kj_elek     (BO) := float64(KanGebiedsOptie) * V_e * Prijzen_elec/KGJ_eindgebruik_excl[classify(Installatie/metervraag/e + BO/Functioneel/EA * Invoer/Kengetallen/Constanten/Efficiency_EA, Prijzen_elec/ClassBreak )];
	attribute<Eur_yr> Km_elek     (BO) := float64(KanGebiedsOptie) * V_e * Prijzen_elec/KGJ_Maatschappelijk [classify(Installatie/metervraag/e + BO/Functioneel/EA * Invoer/Kengetallen/Constanten/Efficiency_EA, Prijzen_elec/ClassBreak )];

	attribute<Eur_yr> Kj_gas_EH   (BO) := float64(KanGebiedsOptie) * V_gas * Prijzen_gas/KGJ_EnergieHeffing  [classify(Installatie/metervraag/gas, Prijzen_gas/ClassBreak )];
	attribute<Eur_yr> Kj_gas_CO2  (BO) := float64(KanGebiedsOptie) * V_gas * Prijzen_gas/KGJ_CO2Heffing      [classify(Installatie/metervraag/gas, Prijzen_gas/ClassBreak )];
	attribute<Eur_yr> Kj_gas      (BO) := float64(KanGebiedsOptie) * V_gas * Prijzen_gas/KGJ_eindgebruik_excl[classify(Installatie/metervraag/gas, Prijzen_gas/ClassBreak )];
	attribute<Eur_yr> Km_gas      (BO) := float64(KanGebiedsOptie) * V_gas * Prijzen_gas/KGJ_Maatschappelijk [classify(Installatie/metervraag/gas, Prijzen_gas/ClassBreak )];
	
	parameter<Eur_asl> K_Aansl_asl := IsWoning ? Kentallen/K_aansl_w : 0[Eur_asl];
	parameter<Eur_m2>  K_Aansl_opp := IsWoning ? 0[Eur_m2] : Kentallen/K_aansl_u;

	attribute<Eur>    Ki_aansl    (BO) := float64(KanGebiedsOptie) * max_elem(K_Aansl_asl * nrAansluitingen + K_Aansl_opp * oppervlakte, Kentallen/K_aansl_w * nrAansluitingen)[Eur];
	attribute<Eur>    Ki_LO       (BO) := float64(KanGebiedsOptie) * Installatie/Ki_LO;
	attribute<Eur_yr> Kji_LO      (BO) := float64(KanGebiedsOptie) * Installatie/Kji_LO;
	attribute<Eur_yr> Kmi_LO      (BO) := float64(KanGebiedsOptie) * Installatie/Kmi_LO;
	attribute<Eur_yr> Kj_oh_LO    (BO) := float64(KanGebiedsOptie) * Installatie/Kj_oh_LO;

	container PerPlanRegio
	{
			attribute<nrAsl>  Aansluitingen   (PlanRegio) := sum(nrAansluitingen, Planregio_rel);

			//VERBRUIK
			attribute<GJ_yr>  V_Warmte        (PlanRegio) := sum((V_RV + V_TW)  , Planregio_rel);
			attribute<GJ_yr>  V_Waterstof     (PlanRegio) := sum(V_H2           , Planregio_rel);
			attribute<GJ_yr>  V_Gas           (PlanRegio) := sum(../V_gas       , Planregio_rel);
			attribute<GJ_yr>  V_id_elek       (PlanRegio) := sum(V_e            , Planregio_rel);

			//KOSTEN
			attribute<Eur_yr> Kj_id_gas       (PlanRegio) := sum(Kj_gas         , Planregio_rel);
			attribute<Eur_yr> Km_id_gas       (PlanRegio) := sum(Km_gas         , Planregio_rel);
			attribute<Eur_yr> Kj_id_gas_EH    (PlanRegio) := sum(Kj_gas_EH      , Planregio_rel);
			attribute<Eur_yr> Kj_id_gas_CO2   (PlanRegio) := sum(Kj_gas_CO2     , Planregio_rel);
			
			//KOSTEN
			attribute<Eur_yr> Kj_id_elek      (PlanRegio) := sum(Kj_elek        , Planregio_rel);
			attribute<Eur_yr> Km_id_elek      (PlanRegio) := sum(Km_elek        , Planregio_rel);
			attribute<Eur_yr> Kj_id_elek_EH   (PlanRegio) := sum(Kj_elek_EH     , Planregio_rel);
			attribute<Eur_yr> Kj_id_elek_CO2  (PlanRegio) := sum(Kj_elek_CO2    , Planregio_rel);

			attribute<Eur>    Ki_ge_pm        (PlanRegio) := Aansluitingen * Kentallen/K_pm * float64(!IsNieuwbouw);
			attribute<Eur>    Ki_ge_ov        (PlanRegio) := Aansluitingen * Kentallen/K_ov * float64(!IsNieuwbouw);

			attribute<Eur>    Ki_Aansl        (PlanRegio) := sum(../Ki_Aansl    , Planregio_rel);
			attribute<Eur>    Ki_LO           (PlanRegio) := sum(../Ki_LO       , Planregio_rel);
			attribute<Eur_yr> Kji_LO          (PlanRegio) := sum(../Kji_LO      , Planregio_rel);
			attribute<Eur_yr> Kmi_LO          (PlanRegio) := sum(../Kmi_LO      , Planregio_rel);
			attribute<Eur_yr> Kj_oh_LO        (PlanRegio) := sum(../Kj_oh_LO    , Planregio_rel);
	}
}